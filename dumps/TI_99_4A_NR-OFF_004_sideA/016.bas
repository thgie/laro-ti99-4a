    5 RANDOMIZE  :: CALL CLEAR 
   10 PRINT TAB ( 11 )  ; "HENKER" 
   20 PRINT "     CREATIVE COMPUTING      MORRISTOWN,NEW JERSEY,USA" 
   25 PRINT  :  :  : 
   30 DIM P$ ( 12 , 12 ) , L$ ( 21 ) , D$ ( 21 ) , N$ ( 26 ) , U ( 70 ) 
   40 C = 1  :: N = 70 
   50 FOR I = 1 TO 21  :: D$ ( I ) = "-"  :: NEXT I  :: M = 0 
   60 FOR I = 1 TO 26  :: N$ ( I ) = ""  :: NEXT I 
   70 FOR I = 1 TO 12  :: FOR J = 1 TO 12  :: P$ ( I , J ) = " "  :: NEXT J  :: NEXT I 
   80 FOR I = 1 TO 12  :: P$ ( I , 1 ) = "X"  :: NEXT I 
   90 FOR I = 1 TO 7  :: P$ ( 1 , I ) = "X"  :: NEXT I  :: P$ ( 2 , 7 ) = "X" 
   95 IF C < N THEN 100
   97 PRINT "DU HAST ALLE WOERTER        GELOEST !!"  :: GOTO 990
  100 Q = INT ( N * RND ) + 1 
  110 IF U ( Q ) = 1 THEN 100
  115 U ( Q ) = 1  :: C = C + 1  :: RESTORE  :: T1 = 0 
  150 FOR I = 1 TO Q  :: READ A$  :: NEXT I 
  160 L = LEN ( A$ )  :: FOR I = 1 TO L  :: L$ ( I ) = SEG$ ( A$ , I , 1 )  :: NEXT I 
  170 PRINT "DIE FOLGENDEN BUCHSTABEN    WURDEN BENUTZT:" 
  180 FOR I = 1 TO 26  :: PRINT N$ ( I )  ;  :: IF N$ ( I + 1 ) = "" THEN 200
  190 PRINT ","  ;  :: NEXT I 
  200 PRINT  :  :  :: FOR I = 1 TO L  :: PRINT D$ ( I )  ;  :: NEXT I  :: PRINT  :  : 
  210 INPUT "WAS RAETST DU?:"  : G$  :: R = 0 
  220 FOR I = 1 TO 26  :: IF N$ ( I ) = "" THEN 250
  230 IF G$ = N$ ( I ) THEN PRINT "DU HAST DIESEN BUCHSTABEN   SCHON MAL VERSUCHT."  :: GOTO 170
  240 NEXT I  :: PRINT "PROGRAMMFEHLER.VERSUCHE NOCHMAL."  :: STOP 
  250 N$ ( I ) = G$  :: T1 = T1 + 1 
  260 FOR I = 1 TO L  :: IF L$ ( I ) = G$ THEN 280
  270 NEXT I  :: IF R = 0 THEN 290
  275 GOTO 300
  280 D$ ( I ) = G$  :: R = R + 1  :: GOTO 270
  290 M = M + 1  :: GOTO 400
  300 FOR I = 1 TO L  :: IF D$ ( I ) = "-" THEN 320
  310 NEXT I  :: GOTO 390
  320 PRINT  :: FOR I = 1 TO L  :: PRINT D$ ( I )  ;  :: NEXT I  :: PRINT  :  : 
  330 INPUT "WAS RAETST DU FUER DIESES   WORT?:"  : B$ 
  340 IF A$ = B$ THEN 360
  350 PRINT "FALSCH,VERSUCHE EINEN       ANDEREN BUCHSTABEN..."  :  :: GOTO 170
  360 PRINT "RICHTIG!! DU HAST"  ; T1  ; "VERSUCHE"  ; "GEBRAUCHT!" 
  370 INPUT "NOCH EIN WORT?:"  : W$  :: IF W$ = "JA" THEN 50
  380 PRINT  :  : "ES HAT MIR GEFALLEN.        BIS SPAETER."  :: GOTO 999
  390 PRINT "DU HAST DAS WORT ERRATEN!"  :: GOTO 370
  400 PRINT  :  : "SCHADE DIESER BUCHSTABE IST NICHT ENTHALTEN." 
  410 ON M GOTO 415, 420, 425, 430, 435, 440, 445, 450, 455, 460
  415 PRINT "ZUERST ZEICHNEN WIR DEN KOPF"  :: GOTO 470
  420 PRINT "JETZT ZEICHNEN WIR DEN      KOERPER."  :: GOTO 470
  425 PRINT "DANN ZEICHNEN WIR EINEN ARM."  :: GOTO 470
  430 PRINT "JETZT DEN ANDEREN ARM."  :: GOTO 470
  435 PRINT "JETZT DAS RECHTE BEIN."  :: GOTO 470
  440 PRINT "JETZT DAS LINKE BEIN."  :: GOTO 470
  445 PRINT "JETZT EINE HAND"  :: GOTO 470
  450 PRINT "JETZT DIE ANDERE HAND."  :: GOTO 470
  455 PRINT "JETZT ZEICHNEN WIR EINEN    FUSS."  :: GOTO 470
  460 PRINT "HIER IST DER ANDERE FUSS--DUBIST GEHENKT!!" 
  470 ON M GOTO 480, 490, 500, 510, 520, 530, 540, 550, 560, 570
  480 P$ ( 3 , 6 ) = "-"  :: P$ ( 3 , 7 ) = "-"  :: P$ ( 3 , 8 ) = "-"  :: P$ ( 4 , 5 ) = "("  :: P$ ( 4 , 6 ) = "." 
  481 P$ ( 4 , 8 ) = "."  :: P$ ( 4 , 9 ) = ")"  :: P$ ( 5 , 6 ) = "-"  :: P$ ( 5 , 7 ) = "-"  :: P$ ( 5 , 8 ) = "-"  :: GOTO 580
  490 FOR I = 6 TO 9  :: P$ ( I , 7 ) = "X"  :: NEXT I  :: GOTO 580
  500 FOR I = 4 TO 7  :: P$ ( I , I - 1 ) = "\"  :: NEXT I  :: GOTO 580
  510 FOR I = 4 TO 7  :: P$ ( I , 15 - I ) = "/"  :: NEXT I  :: GOTO 580
  520 P$ ( 10 , 6 ) = "/"  :: P$ ( 11 , 5 ) = "/"  :: GOTO 580
  530 P$ ( 10 , 8 ) = "\"  :: P$ ( 11 , 9 ) = "\"  :: GOTO 580
  540 P$ ( 3 , 11 ) = "\"  :: GOTO 580
  550 P$ ( 3 , 3 ) = "/"  :: GOTO 580
  560 P$ ( 12 , 10 ) = "\"  :: P$ ( 12 , 11 ) = "_"  :: GOTO 580
  570 P$ ( 12 , 3 ) = "_"  :: P$ ( 12 , 4 ) = "/" 
  580 FOR I = 1 TO 12  :: FOR J = 1 TO 12  :: PRINT P$ ( I , J )  ;  :: NEXT J 
  590 PRINT  :: NEXT I  :: PRINT  :  :  :: IF M < > 10 THEN 170
  600 PRINT "SCHADE,DU VERLIERST. DAS    WORT WAR "  ; A$ 
  610 PRINT "DU HAST DIESMAL VERLOREN."  ;  :: GOTO 370
  620 INPUT "TIPPE JA ODER NEIN:"  : Y$  :: IF Y$ = "JA" THEN 50
  700 DATA TON , ROT , BEI , BUS , HEU , DAS , MAI 
  710 DATA LIEB , JEDE , REDE , EHRE , PAKT , ULME , SACK 
  720 DATA PIZZA , PUDER , BOGEN , POKAL , LINDE , KOHLE , JAGEN 
  730 DATA HELFEN , HEILIG , GIPFEL , FRIEDE , EISERN , BIEGEN 
  740 DATA BESEHEN , AUSSAAT , AUFTRAG , MUSCHEL , LAMETTA , KONTAKT 
  750 DATA KINDHEIT , IMPFARZT , HIMBEERE , GESCHICK , EXISTENZ 
  760 DATA HEERESZUG , GEFAEHRTE , SCHACHTEL , DIFFERENZ , GLEICHMUT 
  770 DATA SEELENRUHE , FEHLERHAFT , VERLAUTBAR , PASSATWIND 
  780 DATA VERHEIRATET , GIROVERBINDLICHKEITEN , INDUSTRIALISMUS 
  790 DATA PERSOEHNLICHKEIT , EINFLUSS , EINGABE , STELLENWERT , MOEGLICHKEIT , THEORIE , HIER , MUT , INDUSTRIEZONE , ZEITPUNKT 
  800 DATA COMPUTERSPIELE , DA , RHYTHMUS , FUSION , KOMPLEXITAET , MAKLER , SATANSBRATEN , RECHTSWIDRIGKEIT , GASCHROMATOGRAPHIE , UMWELTVERSCHMUTZUNG 
  990 PRINT "BIS SPAETER" 
  999 END 
