   10 RANDOMIZE  ! SUPER STARTREK
  220 CALL CLEAR 
  221 AX$ = "               ,-----*-----,,-----------                 ,______  _,    '---/ /---'" 
  222 AY$ = "     ,___\ \________/ /__,                                   '------------------'    DIE USS ENTERPRISE--NCC-1071" 
  227 DISPLAY AT ( 6 , 1 )  : AX$ & AY$ 
  260  :: 
  270 Z$ = "" 
  330 DIM G ( 8 , 8 ) , C ( 9 , 2 ) , K ( 3 , 3 ) , N ( 3 ) , Z ( 8 , 8 ) , D ( 8 ) 
  370 T = INT ( RND * 20 + 20 ) * 100  :: T0 = T  :: T9 = 25 + INT ( RND * 10 )  :: D0 = 0  :: E = 3000  :: E0 = 0 
  440 P , P0 = 10  :: S9 = 200  :: S , B9 , K9 = 0  :: X$ = ""  :: X0$ = " IST" 
  470 DEF FND ( D ) = SQR ( ( K ( I , 1 ) - S1 ) ^ 2 + ( K ( I , 2 ) - S2 ) ^ 2 ) 
  475 DEF FNR ( R ) = INT ( ( RND * R ) * 7.98 + 1.01 ) 
  480  !  AUSGANGSLAGE ENTERPRISE
  490 Q1 = FNR ( 1 )  :: Q2 = FNR ( 1 )  :: S1 = FNR ( 1 )  :: S2 = FNR ( 1 ) 
  530 FOR I = 1 TO 9  :: C ( I , 1 ) , C ( I , 2 ) = 0  :: NEXT I 
  540 C ( 3 , 1 ) , C ( 2 , 1 ) , C ( 4 , 1 ) , C ( 4 , 2 ) , C ( 5 , 2 ) , C ( 6 , 2 ) = - 1 
  600 C ( 1 , 2 ) , C ( 2 , 2 ) , C ( 6 , 1 ) , C ( 7 , 1 ) , C ( 8 , 1 ) , C ( 8 , 2 ) , C ( 9 , 2 ) = 1 
  670 FOR I = 1 TO 8  :: D ( I ) = 0  :: NEXT I 
  710 A1$ = "NAVSRSLRSPHATORSHEDAMCOMXXX" 
  810 REM  WELTRAUMAUFSTELLUNG
  815 REM  K3=KLINGONS,B3=STAT.S3=STERNE
  820 FOR I = 1 TO 8  :: FOR J = 1 TO 8  :: K3 , Z ( I , J ) = 0  :: R1 = RND 
  850 IF R1 > .98 THEN K3 = 3  :: K9 = K9 + 3  :: GOTO 980
  860 IF R1 > .95 THEN K3 = 2  :: K9 = K9 + 2  :: GOTO 980
  870 IF R1 > .8 THEN K3 = 1  :: K9 = K9 + 1 
  980 B3 = 0  :: IF RND > .96 THEN B3 = 1  :: B9 = B9 + 1 
 1040 G ( I , J ) = K3 * 100 + B3 * 10 + FNR ( 1 )  :: NEXT J  :: NEXT I  :: IF K9 > T9 THEN T9 = K9 + 1 
 1100 IF B9 THEN 1200
 1150 IF G ( Q1 , Q2 ) < 200 THEN G ( Q1 , Q2 ) = G ( Q1 , Q2 ) + 100  :: K9 = K9 + 1 
 1160 B9 = 1  :: G ( Q1 , Q2 ) = G ( Q1 , Q2 ) + 10  :: Q1 = FNR ( 1 )  :: Q2 = FNR ( 1 ) 
 1200 K7 = K9  :: IF B9 < > 1 THEN X$ = "S"  :: X0$ = " SIND" 
 1230 PRINT "DU HAST FOLG. ANWEISUNGEN:" 
 1240 PRINT "ZERSTOERE DIE"  ; K9  ; "KLINGON KAMPFSCHIFFE," 
 1250 PRINT "DIE DEN WELTRAUM AUFRUEHREN,BEVOR SIE DAS HAUPTQUARTIER DER FOEDERATION AM STERNTAG"  ; T0 + T9  ; "ANGREIFEN KOENNEN." 
 1260 PRINT "ES BLEIBEN DIR"  ; T9  ; "TAGE."  : "ES"  ; X0$  ; B9  ; "STAT. IM RAUM,"  : "AN DENEN DU DEIN SCHIFF     AUFLADEN KANNST.  WENN DU   BEREIT BIST," 
 1270 PRINT "DRUECKE EINE TASTE." 
 1300 I = RND  :: CALL KEY ( 0 , KEY , STATUS )  :: IF STATUS = 0 THEN 1300
 1310 REM  NEUER QUADRANT
 1320 Z4 = Q1  :: Z5 = Q2  :: K3 , B3 , S3 , G5 = 0  :: D4 = .5 * RND  :: Z ( Q1 , Q2 ) = G ( Q1 , Q2 ) 
 1390 IF Q1 < 1 OR Q1 > 8 OR Q2 < 1 OR Q2 > 8 THEN 1600
 1430 GOSUB 9030 :: PRINT  :: IF T0 < > T THEN 1490
 1460 PRINT "ZU BEGINN DER MISSION       BEFINDET SICH DAS RAUMSCHIFFIM QUADRANTEN "  ; G2$  ; "."  :: GOTO 1500
 1490 PRINT "EINTRITT IN DEN QUADRANTEN "  ; G2$  ; "..." 
 1500 PRINT  :: K3 = INT ( G ( Q1 , Q2 ) * .01 )  :: B3 = INT ( G ( Q1 , Q2 ) * .1 ) - 10 * K3 
 1540 S3 = G ( Q1 , Q2 ) - 100 * K3 - 10 * B3  :: IF K3 = 0 THEN 1590
 1560 PRINT "KAMPFBEREICH ZUSTAND ROT"  :: IF S > 200 THEN 1590
 1580 PRINT "ABWEHRSCHILDE GEFAEHRLICH   NIEDRIG" 
 1590 FOR I = 1 TO 3  :: K ( I , 1 ) , K ( I , 2 ) = 0  :: NEXT I 
 1600 FOR I = 1 TO 3  :: K ( I , 3 ) = 0  :: NEXT I  :: Q$ = RPT$ ( Z$ , 7 ) & SEG$ ( Z$ , 1 , 17 ) 
 1660 REM  POSITIONIERE ENTERPRISE IM QUADRANTEN DANN KLINGONS STATIONEN UND STERNE
 1680 A$ = "<*>"  :: Z1 = S1  :: Z2 = S2  :: GOSUB 8670 :: IF K3 < 1 THEN 1820
 1720 FOR I = 1 TO K3  :: GOSUB 8590 :: A$ = "+K+"  :: Z1 = R1  :: Z2 = R2 
 1780 GOSUB 8670 :: K ( I , 1 ) = R1  :: K ( I , 2 ) = R2  :: K ( I , 3 ) = S9 * ( .5 * RND )  :: NEXT I 
 1820 IF B3 < 1 THEN 1910
 1880 GOSUB 8590 :: A$ = ">!<"  :: Z1 , B4 = R1  :: Z2 , B5 = R2  :: GOSUB 8670
 1910 FOR I = 1 TO S3  :: GOSUB 8590 :: A$ = " * "  :: Z1 = R1  :: Z2 = R2  :: GOSUB 8670 :: NEXT I 
 1980 GOSUB 6430
 1990 IF S + E > 10 THEN IF E > 10 OR D ( 7 ) = 0 THEN 2060
 2020 PRINT "**** ERNSTHAFTER FEHLER ****DU BIST GERADE MIT DEM      SCHIFF IM RAUM GESTRANDET.  DU HAST UNGEN. MANOEVRIER-" 
 2030 PRINT "ENERGIE UND SCHILDKONTROLLE IST GEGENWAERTIG UNFAEHIG,  ZUM MASCHINENRAUM UMZUSCHAL-TEN!!!!"  :: GOTO 6220
 2060 LINPUT "BEFEHL?:"  : A$ 
 2080 FOR I = 1 TO 9  :: IF SEG$ ( A$ , 1 , 3 ) < > SEG$ ( A1$ , 3 * I - 2 , 3 ) THEN 2160
 2140 ON I GOTO 2300, 1980, 4000, 4260, 4700, 5530, 5690, 7290, 6270
 2160 NEXT I  :: PRINT "TIPPE EINEN DER FOLGENDEN   BEFEHLE:"  : "NAV(RICHTUNGSAENDERUNG)"  : "SRS(NAHBEREICHSUEBERSICHT)" 
 2200 PRINT "LRS(FERNBEREICHSUEBERSICHT) PHA(PHASER)"  : "TOR(PHOTONEN-TORPEDOS)"  : "SHE(SCHILDE)"  : "DAM(ZERSTOERUNGSBERICHT)"  : "COM(SPEICHERCOMPUTER)"  : "XXX(BEFEHLSABBRUCH)" 
 2260 PRINT  :: GOTO 1990
 2290 REM  KURSKONTROLLE
 2300 INPUT "KURS(1-9)?:"  : C1  :: IF C1 = 9 THEN C1 = 1 
 2310 IF C1 > = 1 AND C1 < 9 THEN 2350
 2330 PRINT "LEUTNANT SULU BERICHTET:    'FALSCHE KURSDATEN'"  :: GOTO 1990
 2350 X$ = "8"  :: IF D ( 1 ) < 0 THEN X$ = "0.2" 
 2360 INPUT "FAKTOR(0-" & X$ & ")?:"  : W1  :: IF D ( 1 ) < 0 AND W1 > .2 THEN 2470
 2380 IF W1 > 0 AND W1 < = 8 THEN 2490
 2390 IF W1 = 0 THEN 1990
 2420 PRINT "CHEFINGENIEUR KOCH BERICHTET'DIE MASCHINEN VERKRAFTEN   NICHT FAKTOR"  ; W1  ; "!'"  :: GOTO 1990
 2470 PRINT "RICHTUNGSMOTOREN SIND       ZERSTOERT.HOECHSTGESCHW.=0.2"  :: GOTO 1990
 2490 NN = INT ( W1 * 8 + .5 )  :: IF E - NN > = 0 THEN 2590
 2500 PRINT "MASCHINISTEN BERICHTEN:     'UNGENUEGEND ENERGIE UM MIT GESCHW."  ; W1  ; "ZU MANOEVRIEREN." 
 2530 IF S < NN - E OR D ( 7 ) < 0 THEN 1990
 2550 PRINT "ABSCHIRMKONTROLLRAUM        BESTAETIGT:"  : S  ; "ENERGIEHEITEN FUER"  ; "SCHILDE VERWENDET." 
 2570 GOTO 1990
 2580 REM  KLINGONS
 2590 FOR I = 1 TO K3  :: IF K ( I , 3 ) = 0 THEN 2700
 2610 A$ = "   "  :: Z1 = K ( I , 1 )  :: Z2 = K ( I , 2 )  :: GOSUB 8670 :: GOSUB 8590
 2660 K ( I , 1 ) = Z1  :: K ( I , 2 ) = Z2  :: A$ = "+K+"  :: GOSUB 8670
 2700 NEXT I  :: GOSUB 6000 :: D1 = 0  :: D6 = W1  :: IF W1 > = 1 THEN D6 = 1 
 2770 FOR I = 1 TO 8  :: IF D ( I ) > = 0 THEN 2880
 2790 D ( I ) = D ( I ) + D6  :: IF D ( I ) > - .1 AND D ( I ) < 0 THEN D ( I ) = - .1  :: GOTO 2880
 2800 IF D ( I ) < 0 THEN 2880
 2810 D1 = 1  :: PRINT "ZERSTOERUNGSBERICHT:" 
 2840 PRINT TAB ( 1 )  ;  :: R1 = I  :: GOSUB 8790 :: PRINT G2$  ; "REPERATUR FERTIG" 
 2880 NEXT I  :: IF RND > .2 THEN 3070
 2910 R1 = FNR ( 1 )  :: IF RND > = .6 THEN 3000
 2930 D ( R1 ) = D ( R1 ) - ( RND * 5 + 1 )  :: PRINT "ZERSTOERUNGSBERICHT:" 
 2960 GOSUB 8790 :: PRINT G2$  ; " ZERSTOERT"  :  :  :: GOTO 3070
 3000 D ( R1 ) = D ( R1 ) + RND * 3 + 1  :: PRINT "ZERSTOERUNGSBERICHT:" 
 3030 GOSUB 8790 :: PRINT G2$  ; " REP.ZUSTAND VERB."  :  : 
 3060 REM  BEWEGUNG
 3070 A$ = "   "  :: Z1 = INT ( S1 )  :: Z2 = INT ( S2 )  :: GOSUB 8670
 3110 X1 = C ( C1 , 1 ) - ( C ( C1 + 1 , 1 ) - C ( C1 , 1 ) ) * ( C1 - INT ( C1 ) )  :: X = S1  :: Y = S2 
 3140 X2 = C ( C1 , 2 ) - ( C ( C1 + 1 , 2 ) - C ( C1 , 2 ) ) * ( C1 - INT ( C1 ) ) 
 3170 FOR I = 1 TO NN  :: S1 = S1 + X1  :: S2 = S2 + X2  :: IF S1 < 1 OR S1 > = 9 OR S2 < 1 OR S2 > = 9 THEN 3500
 3240 S8 = INT ( S1 ) * 24 + INT ( S2 ) * 3 - 26  :: IF SEG$ ( Q$ , S8 , 2 ) = "  " THEN 3360
 3320 S1 = INT ( S1 - X1 )  :: S2 = INT ( S2 - X2 )  :: PRINT "LENKMOTOREN AUSGESCHALTEN IN" 
 3350 PRINT "SEKTOR"  ; S1  ; ","  ; S2  ; "WEGEN SCHLECHTER NAVIGATION"  :: GOTO 3370
 3360 NEXT I  :: S1 = INT ( S1 )  :: S2 = INT ( S2 ) 
 3370 A$ = "<*>"  :: Z1 = INT ( S1 )  :: Z2 = INT ( S2 )  :: GOSUB 8670 :: GOSUB 3910 :: T8 = 1 
 3430 IF W1 < 1 THEN T8 = .1 * INT ( 10 * W1 ) 
 3450 T = T + T8  :: IF T > T0 + T9 THEN 6220
 3480 GOTO 1980
 3490 REM  QUADRANTGRENZEN UEBERSCHRITTEN
 3500 X = 8 * Q1 + X + NN * X1  :: Y = 8 * Q2 + Y + NN * X2  :: Q1 = INT ( X / 8 )  :: Q2 = INT ( Y / 8 )  :: S1 = INT ( X - Q1 * 8 )  :: S2 = INT ( Y - Q2 * 8 )  :: IF S1 = 0 THEN Q1 = Q1 - 1  :: S1 = 8 
 3590 IF S2 = 0 THEN Q2 = Q2 + 1  :: S2 = 8 
 3620 X5 = 0  :: IF Q1 < 1 THEN X5 , Q1 , S1 = 1 
 3670 IF Q1 > 8 THEN X5 = 1  :: Q1 , S1 = 8 
 3710 IF Q2 < 1 THEN X5 , Q2 , S2 = 1 
 3750 IF Q2 > 8 THEN X5 = 1  :: Q2 , S2 = 8 
 3790 IF X5 = 0 THEN 3860
 3800 PRINT "LT. UHURA BERICHTET MELDUNG VOM FLOTTENKOMMANDO:        'ERLAUBNIS ZUM VERSUCH DIE   AEUSSEREN GRENZEN DES" 
 3810 PRINT " WELTALLS ZU VERLASSEN,WIRD  HIERMIT *VERWEIGERT*.       SCHALTE MOTOREN AUS.'"  : "CHEFINGENIEUR KOCH BERICHTET" 
 3820 PRINT ":LENKMOTOREN AUSGESCHALTEN  AUF SEKTOR"  ; S1  ; ","  ; S2  ; "DES QUADRANTEN"  ; Q1  ; ","  ; Q2  ; "." 
 3850 IF T > T0 + T9 THEN 6220
 3860 IF 8 * Q1 + Q2 = 8 * Q4 + Q5 THEN 3370
 3870 T = T + 1  :: GOSUB 3910 :: GOTO 1320
 3900 REM  MANOEVRIERENERGIE
 3910 E = E - NN - 10  :: IF E > = 0 THEN RETURN 
 3930 PRINT "SCHILDKONTROLLE GIBT ENERGIEAB, UM MANOEVER ABZUSCHLIES-SEN." 
 3940 S = S + E  :: E = 0  :: IF S < = 0 THEN S = 0 
 3980 RETURN 
 3990 REM  FERNBEREICHSUEBERSICHT
 4000 IF D ( 3 ) < 0 THEN PRINT "FERNBEREICHSSENSOREN SIND   NICHT BETRIEBSFAEHIG."  :: GOTO 1990
 4030 PRINT "FERNBEREICHS-UEBERSICHT FUERQUADRANT"  ; Q1  ; ","  ; Q2 
 4040 O1$ = "-------------------"  :: PRINT O1$ 
 4060 FOR I = Q1 - 1 TO Q1 + 1  :: N ( 1 ) = - 1  :: N ( 2 ) = - 2  :: N ( 3 ) = - 3  :: FOR J = Q2 - 1 TO Q2 + 1 
 4120 IF I > 0 AND I < 9 AND J > 0 AND J < 9 THEN N ( J - Q2 + 2 ) , Z ( I , J ) = G ( I , J ) 
 4180 NEXT J  :: FOR L = 1 TO 3  :: PRINT ": "  ;  :: IF N ( L ) < = 0 THEN PRINT "*** "  ;  :: GOTO 4230
 4210 PRINT SEG$ ( STR$ ( N ( L ) + 1000 ) , LEN ( STR$ ( N ( L ) + 1000 ) ) - 2 , 3 )  ; " "  ; 
 4230 NEXT L  :: PRINT ":"  : O1$  :: NEXT I  :: GOTO 1990
 4250 REM  PHASERKONTROLLE
 4260 IF D ( 4 ) < 0 THEN PRINT "PHASER NICHT BETRIEBSFAEHIG."  :: GOTO 1990
 4265 IF K3 > 0 THEN 4330
 4270 PRINT "TECHN. OFFI. SPOCK BERICHTET:'FUEHLER ZEIGEN KEINE      FEINDESSCHIFFE IM QUADRANTEN"  :: GOTO 1990
 4330 IF D ( 8 ) < 0 THEN PRINT "COMPUTERFEHLER VERFAELSCHT  GENAUIGKEIT." 
 4350 PRINT "PHASER AUF ZIEL EINGESTELLT;VERFUEGBARE ENERGIE="  ; E  ; "EINHEITEN." 
 4370 INPUT "WIEVIELE EINHEITEN          ABSCHIESSEN?:"  : X  :: IF X < = 0 THEN 1990
 4400 IF E - X < 0 THEN 4360
 4410 E = E - X  :: IF D ( 7 ) < 0 THEN X = X * RND 
 4450 H1 = INT ( X / K3 )  :: FOR I = 1 TO 3  :: IF K ( I , 3 ) < = 0 THEN 4670
 4480 H = INT ( ( H1 / FND ( 0 ) ) * ( RND + 2 ) )  :: IF H > .15 * K ( I , 3 ) THEN 4530
 4500 PRINT "FUEHLER BERICHTEN KEINE     BESCHAEDIGUNG DES FEINDES   AUF"  ; K ( I , 1 )  ; ","  ; K ( I , 2 )  :: GOTO 4670
 4530 K ( I , 3 ) = K ( I , 3 ) - H  :: PRINT "KLINGON AUF SEKTOR"  ; K ( I , 1 )  ; ","  ; K ( I , 2 )  ; "MIT"  ; H  ; "EINHEITEN GETROFFEN." 
 4550 IF K ( I , 3 ) < = 0 THEN PRINT "***KLINGON ZERSTOERT***"  :: GOTO 4580
 4560 PRINT "(FUEHLER ZEIGEN:"  : " NOCH"  ; K ( I , 3 )  ; "EINHEITEN"  : " UEBRIG)"  :: GOTO 4670
 4580 K3 = K3 - 1  :: K9 = K9 - 1  :: Z1 = K ( I , 1 )  :: Z2 = K ( I , 2 )  :: A$ = "   "  :: GOSUB 8670
 4650 K ( I , 3 ) = 0  :: G ( Q1 , Q2 ) , Z ( Q1 , Q2 ) = G ( Q1 , Q2 ) - 100  :: IF K9 < = 0 THEN 6370
 4670 NEXT I  :: GOSUB 6000 :: GOTO 1990
 4690 REM  PHOT.-TORPEDO
 4700 IF P < = 0 THEN PRINT "ALLE PHOTONENTORPEDOS       VERBRAUCHT."  :: GOTO 1990
 4730 IF D ( 5 ) < 0 THEN PRINT "PHOTONENROEHREN SIND AUSSER BETRIEB."  :: GOTO 1990
 4760 INPUT "KURS DES TORPEDOS(1-9)?:"  : C1  :: IF C1 = 9 THEN C1 = 1 
 4780 IF C1 > = 1 AND C1 < 9 THEN 4850
 4790 PRINT "FAEHNRICH CHEKOVBERICHTET:  'FALSCHE KURSDATEN,SIR!'"  :: GOTO 1990
 4850 X1 = C ( C1 , 1 ) + ( C ( C1 + 1 , 1 ) - C ( C1 , 1 ) ) * ( C1 - INT ( C1 ) )  :: E = E - 2  :: P = P - 1 
 4860 X2 = C ( C1 , 2 ) + ( C ( C1 + 1 , 2 ) - C ( C1 , 1 ) ) * ( C1 - INT ( C1 ) )  :: X = S1  :: Y = S2 
 4910 PRINT "TORPEDOBAHN:" 
 4920 X = X + X1  :: Y = Y + X2  :: X3 = INT ( X + .5 )  :: Y3 = INT ( Y + .5 ) 
 4960 IF X3 < 1 OR X3 > 8 OR Y3 < 1 OR Y3 > 8 THEN 5490
 5000 PRINT "   "  ; X3  ; ","  ; Y3  :: A$ = "   "  :: Z1 = X  :: Z2 = Y  :: GOSUB 8830
 5050 IF Z3 THEN 4920
 5060 A$ = "+K+"  :: Z1 = X  :: Z2 = Y  :: GOSUB 8830 :: IF Z3 = 0 THEN 5210
 5110 PRINT "***KLINGON ZERSTOERT***"  :: K3 = K3 - 1  :: K9 = K9 - 1  :: IF K9 < = 0 THEN 6370
 5150 FOR I = 1 TO 3  :: IF X3 = K ( I , 1 ) AND Y3 = K ( I , 2 ) THEN 5190
 5180 NEXT I  :: I = 3 
 5190 K ( I , 3 ) = 0  :: GOTO 5430
 5210 A$ = " * "  :: Z1 = X  :: Z2 = Y  :: GOSUB 8830 :: IF Z3 = 0 THEN 5280
 5260 PRINT "STERN AUF"  ; X3  ; ","  ; Y3  ; "ABSORBIERTE"  : "TORPEDOENERGIE."  :: GOSUB 6000 :: GOTO 1990
 5280 A$ = ">!<"  :: Z1 = X  :: Z2 = Y  :: GOSUB 8830 :: IF Z3 = 0 THEN 4760
 5330 PRINT "**STERNENSTATION ZERSTOERT**"  :: B3 = B3 - 1  :: B9 = B9 - 1 
 5370 PRINT "SO GESCHIEHT ES,KAPITAEN!!  DAMIT VERLIERST DU DAS"  : "KOMMANDO UND WIRST ZU 99"  : "STERNENTAGEN HARTER ARBEIT  AUF CYGNUS STRAFVERSETZT!!" 
 5390 GOTO 6270
 5400 PRINT "FLOTTENKOMMANDO UEBERPRUEFT DEINE DATEN FUER DAS"  : "MILLITAERGERICHT."  :: D0 = 0 
 5430 Z1 = X  :: Z2 = Y  :: A$ = "   "  :: GOSUB 8670
 5470 G ( Q1 , Q2 ) , Z ( Q1 , Q2 ) = K3 * 100 + B3 * 10 + S3  :: GOSUB 6000 :: GOTO 1990
 5490 PRINT "TORPEDO VERFEHLTE"  :: GOSUB 6000 :: GOTO 1990
 5520 REM  SCHILDKONTROLLE
 5530 IF D ( 7 ) < 0 THEN PRINT "SCHILDKONTROLLE AUSSER      BETRIEB."  :: GOTO 1990
 5560 PRINT "VERFUEGBARE ENERGIE="  ; E + S  ;  :: INPUT "WIEVIELE EINHEITEN FUER     SCHILDE?:"  : X 
 5580 IF X < 0 OR S = X THEN PRINT "<SCHILDE UNVERAENDERT>"  :: GOTO 1990
 5590 IF X < = E + S THEN 5630
 5600 PRINT "SCHILDKONTROLLE BERICHTET:  DIES IST NICHT DAS FINANZAMTDER FOEDERATION." 
 5610 PRINT "<SCHILDE UNVERAENDERT>"  :: GOTO 1990
 5630 E = E + S - X  :: S = X  :: PRINT "ABSCHIRMKONTROLLE BERICHTET:" 
 5660 PRINT "SCHILDE BENOETIGEN JETZT"  ; INT ( S )  ; "EINHEITEN PRO BEFEHL."  :: GOTO 1990
 5680 REM  ZERSTOERUNGSUEBERWACHUNG
 5690 IF D ( 6 ) > = 0 THEN 5910
 5700 PRINT "ZERSTOERUNGS-UEBERSICHT     NICHT VERFUEGBAR."  :: IF D0 = 0 THEN 1990
 5730 D3 = 0  :: FOR I = 1 TO 8  :: IF D ( I ) < 0 THEN D3 = D3 + .1 
 5760 NEXT I  :: IF D3 = 0 THEN 1990
 5780 PRINT  :: D3 = D3 + D4  :: IF D3 > = 1 THEN D3 = .9 
 5810 PRINT "TECHNIKER SIND FUER"  : "REPARATIREN DES SCHIFFS"  : "ABRUFBEREIT;"  : "GESCHAETZTE REPARATURDAUER="  ; .01 * INT ( 100 * D3 )  ; "STERNENTAGE." 
 5840 INPUT "GENEHMIGST DU DIE REPARATUR (JA/NEIN)?:"  : A$ 
 5860 IF SEG$ ( A$ , 1 , 1 ) < > "J" THEN 1990
 5870 FOR I = 1 TO 8  :: IF D ( I ) < 0 THEN D ( I ) = 0 
 5890 NEXT I  :: T = T + D3 + .1 
 5910 PRINT "GERAET      REPARATURZUSTAND"  :: FOR R1 = 1 TO 8 
 5920 GOSUB 8790 :: PRINT G2$  ; SEG$ ( Z$ , 1 , 25 - LEN ( G2$ )  ; INT ( D ( R1 ) * 100 ) * .01 
 5950 NEXT R1  :: PRINT  :: IF D0 THEN 5720
 5980 GOTO 1990
 5990 REM  KLINGONS SCHIESSEN
 6000 IF K3 < = 0 THEN RETURN 
 6010 IF D0 THEN PRINT "SCHILDE DER STATION "  : "BESCHUETZEN DIE ENTERPRISE"  :: RETURN 
 6040 FOR I = 1 TO 3  :: IF K ( I , 3 ) < = 0 THEN 6200
 6060 H = INT ( ( K ( I , 3 ) / FND ( 1 ) ) * ( 2 + RND ) )  :: S = S - H  :: K ( I , 3 ) = K ( I , 3 ) / ( 3 + RND ) 
 6080 PRINT "EINHEIT"  ; H  ; "WURDE VOM "  : "SEKTOR"  ; K ( I , 1 )  ; ","  ; K ( I , 2 )  ; "GETROFFEN." 
 6090 IF S < = 0 THEN 6240
 6100 PRINT "<SCHILDE AUF"  ; S  ; "EINHEITEN"  : " VERMINDERT>"  :: IF H < 20 THEN 6200
 6120 IF RND > .6 OR H / S < = .02 THEN 6200
 6140 R1 = FNR ( 1 )  :: D ( R1 ) = D ( R1 ) - H / S - .5 * RND  :: GOSUB 8790
 6170 PRINT "ZERSTOERUNGSKONTROLLE"  : " BERICHTET:'"  ; G2$  ; "DURCH TREFFER BESCHAEDIGT.'" 
 6200 NEXT I  :: RETURN 
 6210 REM  ENDE DES SPIELS
 6220 PRINT "AM STERNENTAG"  ; T  :: GOTO 6270
 6240 PRINT  :  : "DIE ENTERPRISE WURDE"  : "ZERSTOERT.DIE FOEDERATION   WIRD EROBERT"  :: GOTO 6220
 6270 PRINT "AM ENDE DER MISSION SIND    NOCH"  ; K9  ; "KLINGON-"  : "SCHLACHTSCHIFFE UEBRIG." 
 6290 PRINT  :  :  :: IF B9 = 0 THEN 6360
 6310 PRINT "DIE FOEDERATION BENOETIGT   FUER EINE AEHNLICHE AUFGABE EINEN RAUMSCHIFFKOMMANDANTEN-WENN EIN FREIWILLIGER      ANWESEND IST," 
 6330 INPUT "SOLL ER VORKOMMEN UND JAWOHLEINGEBEN"  : A$  :: IF A$ = "JAWOHL" THEN RUN 
 6360 STOP 
 6370 PRINT "GRATULIERE KAPITAEN!"  : "DAS LETZTE SCHLACHTSCHIFF   DER KLINGONS,DAS DIE"  : "FOEDERATION BEDROHTE,WURDE  ZERSTOERT." 
 6400 PRINT "DEINE ERFOLGSBEWERTUNG IST"  : 1000 * ( K7 / ( T - T0 ) ) ^ 2  :: GOTO 6290
 6420  ! SRS
 6430 FOR I = S1 - 1 TO S1 + 1  :: FOR J = S2 - 1 TO S2 + 1 
 6450 IF INT ( I + .5 ) < 1 OR INT ( I + .5 ) > 8 OR INT ( J + .5 ) < 1 OR INT ( J + .5 ) > 8 THEN 6540
 6490 A$ = ">!<"  :: Z1 = I  :: Z2 = J  :: GOSUB 8830 :: IF Z3 = 1 THEN 6580
 6540 NEXT J  :: NEXT I  :: D0 = 0  :: GOTO 6650
 6580 D0 = 1  :: C$ = "ANGELEGT"  :: E = E0  :: P = P0 
 6620 PRINT "SCHILDE FUER ANLEGEMANOEVER GESENKT"  :: S = 0  :: GOTO 6720
 6650 IF K3 > 0 THEN C$ = "*ROT*"  :: GOTO 6720
 6660 C$ = "GRUEN"  :: IF E < E0 * .1 THEN C$ = "GELB" 
 6720 IF D ( 2 ) > = 0 THEN 6770
 6730 PRINT "KURZBEREICHSFUEHLER"  : "AUSGEFALLEN"  :  :  :: RETURN 
 6770 O1$ = "----------------------------"  :: PRINT O1$  :: FOR I = 1 TO 8 
 6820 FOR J = ( I - 1 ) * 24 + 4 TO ( I - 1 ) * 24 + 19 STEP 3  :: PRINT " "  ; SEG$ ( Q$ , J , 3 )  ;  :: NEXT J  :: PRINT ""  :: NEXT I  :: PRINT O1$ 
 6850 PRINT "STERNENTAG"  ; INT ( T * 10 ) * .1  : "ZUSTAND"  ; C$  : "QUADRANT"  ; Q1  ; ","  ; Q2  : "SEKTOR"  ; S1  ; ","  ; S2  : "PHOTONENTORPEDOS"  ; INT ( P )  : "GESAMTENERGIE"  ; INT ( E + S ) 
 7180 PRINT "SCHILDE"  ; INT ( S )  : "KLINGONS UEBRIG"  ; INT ( K9 )  :: RETURN 
 7280  !  SPEICHERCOMPUTER
 7290 IF D ( 8 ) < 0 THEN PRINT "COMPUTER AUSSER BETRIEB."  :: GOTO 1990
 7320 INPUT "COMPUTER AKTIV UND WARTET   AUF BEFEHL:"  : A  :: IF A < 0 THEN 1990
 7350 IF INT ( A + .5 ) < 0 OR INT ( A + .5 ) > 5 THEN 7360 :: H8 = 1  :: ON A + 1 GOTO 7540, 7900, 8070, 8500, 8150, 7400
 7360 PRINT "VERFUEGBARE FUNKTIONEN:"  : "0=WELTRAUMUEBERSICHT"  : "1=ZUSTANDSBERICHT"  : "2=PHOTONEN-TORPEDO DATEN"  : "3=STERNENSTATIONS-NAV-DATEN" 
 7370 PRINT "4=RICHTUNGS/ENTFERNUNGS-RECHNER"  : "5=BEREICHSNAMEN-STERNKARTE"  :  :  :: GOTO 7320
 7390  ! STERNKARTE
 7400 H8 = 0  :: G5 = 1  :: PRINT "                    WELTRAUM"  :: GOTO 7550
 7530  ! WELTRAUMBERICHT
 7890  ! ZUSTANDSBERICHT
 7900 PRINT "ZUSTANDSBERICHT:"  :: X$ = ""  :: IF K9 > 1 THEN X$ = "S" 
 7940 PRINT K9  ; "KLINGON"  ; X$  ; " UEBRIG" 
 7960 PRINT "AUFTRAG MUSS IN"  ; .1 * INT ( ( T0 + T9 - T ) * 10 )  ; "STERNENTAGEN BEENDET SEIN." 
 7970 X$ = "EN"  :: IF B9 < 2 THEN X$ = ""  :: IF B9 < 1 THEN 8010
 7980 PRINT "DIE FOEDERATION UNTERHAELT"  ; B9  ; "STERNENSTATION"  ; X$  ; " IM WELTRAUM." 
 7990 GOTO 5690
 8010 PRINT "WEGEN DEINER DUMMHEIT"  : "VERBLEIBST DU ALLEIN IM"  : "WELTRAUM -- ES IST KEINE    STERNENSTATION UEBRIG!"  :: GOTO 5690
 8060  !  TORPEDO,NAV,R/E ^
 8070 IF K3 < = 0 THEN 4270
 8080 X$ = ""  :: IF K3 > 1 THEN X$ = "E" 
 8090 PRINT "VON DER ENTERPRISE ZU"  : "KLINGONSCHLACHTSCHIFF"  ; X$ 
 8100 H8 = 0  :: FOR I = 1 TO 3  :: IF K ( I , 3 ) < = 0 THEN 8480
 8110 E1 = K ( I , 1 )  :: X = K ( I , 2 ) 
 8120 C1 = S2  :: A = S2  :: GOTO 8220
 8150 PRINT "RICHTUNG/ENTFERNUNG-RECHNER:DU BIST IM QUADRANTEN"  ; Q1  ; ","  ; Q2  ; "SEKTOR"  ; S1  ; ","  ; S2 
 8170 PRINT "BITTE GIB"  :: INPUT "ANFANGSKOORDINATEN EIN(X/Y):"  : C1 , A  :: INPUT "ENDKOORDINATEN EIN(X/Y):"  : W1 , X 
 8220 X = X - A  :: A = C1 - W1  :: IF X < 0 THEN 8350
 8250 IF A < 0 THEN 8410
 8260 IF X > 0 THEN 8280
 8270 IF A = 0 THEN C1 = 5  :: GOTO 8290
 8280 C1 = 1 
 8290 IF ABS ( A ) < = ABS ( X ) THEN 8330
 8310 PRINT "RICHTUNG ="  ; C1 + ( ( ( ABS ( A ) - ABS ( X ) ) + ABS ( A ) / ABS ( A ) )  :: GOTO 8460
 8330 PRINT "RICHTUNG ="  ; C1 + ( ABS ( A ) / ABS ( X ) )  :: GOTO 8460
 8350 IF A > 0 THEN C1 = 3  :: GOTO 8420
 8360 IF X THEN C1 = 5  :: GOTO 8290
 8410 C1 = 7 
 8420 IF ABS ( A ) > = ABS ( X ) THEN 8450
 8430 PRINT "RICHTUNG =" C1 + ( ( ( ABS ( X ) - ABS ( A ) ) + ABS ( X ) / ABS ( X ) )  :: GOTO 8460
 8450 PRINT "RICHTUNG ="  ; C1 + ( ABS ( X ) / ABS ( A ) ) 
 8460 PRINT "ENTFERNUNG="  ; SQR ( X ^ 2 + A ^ 2 )  :: IF H8 = 1 THEN 1990
 8480 NEXT I  :: GOTO 1990
