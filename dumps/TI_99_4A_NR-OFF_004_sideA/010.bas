    1 CALL CLEAR 
    2 PRINT TAB ( 10 )  ; "EINE DAME" 
    4 PRINT "     CREATIVE COMPUTING      MORRISTOWN,NEW JERSEY,USA" 
    6 PRINT  :  :  :  :: FOR I = 1 TO 1000  :: NEXT I 
    8 DIM A ( 64 ) 
   10 PRINT "SOLITAIRE DAME-PUZZLE VON   DAVID AHL."  :  : 
   20 PRINT "48 STEINE WERDEN AUF DIE    BEIDEN AEUSSEREN FELDER     EINES DAMEBRETTES MIT 64    FELDERN GELEGT.DAS ZIEL IST,DURCH DIAGONALE SPRUENGE" 
   30 PRINT "(WIE BEI DAME)SOVIEL STEINE WIE MOEGLICH ZU ENTFERNEN.  VERWENDE DAS NUMMERIERTE    BRETT,UM ANZUGEBEN VON WO   DU WOHIN SPRINGEN WILLST." 
   40 PRINT "AUF DEM SPIELBRETT,DAS NACH JEDEM ZUG AUSGEDRUCKT WIRD, BEDEUTET '1' EIN STEIN UND  '0' EIN LEERES FELD.WENN    KEINE SPRUENGE MEHR MOEGLICH" 
   50 PRINT "SIND,DANN GIB AUF UND GIB   AUF DIE FRAGE 'SPRUNG VON?:'EINE 0 EIN."  :: FOR I = 1 TO 6000  :: NEXT I 
   65 PRINT  :  : "HIER IST DAS NUMMERIERTE    SPIELBRETT:"  :  : 
   70 FOR J = 1 TO 57 STEP 8 
   74 PRINT TAB ( 2 )  ; STR$ ( J )  ; TAB ( 5 )  ; STR$ ( J + 1 )  ; TAB ( 8 )  ; STR$ ( J + 2 )  ; TAB ( 11 )  ; STR$ ( J + 3 )  ; TAB ( 14 )  ; STR$ ( J + 4 )  ; TAB ( 17 )  ; STR$ ( J + 5 )  ; TAB ( 20 )  ; STR$ ( J + 6 )  ; TAB ( 23 )  ; STR$ ( J + 7 ) 
   76 NEXT J 
   78 FOR J = 1 TO 1000  :: NEXT J  :: PRINT  : "UND HIER SIND DIE STEINE ZUMSPIELANFANG:"  :  : 
   80 FOR J = 1 TO 64 
   82 A ( J ) = 1 
   84 NEXT J 
   86 FOR J = 19 TO 43 STEP 8 
   88 FOR I = J TO J + 3 
   90 A ( I ) = 0 
   92 NEXT I  :: NEXT J 
   96 M = 0 
   98 GOTO 340
  100 PRINT "SPRUNG VON ?:"  ;  :: ACCEPT BEEP SIZE ( 2 ) VALIDATE ( DIGIT )  : F 
  105 IF F = 0 THEN 500
  110 PRINT "NACH ?:"  ;  :: ACCEPT BEEP SIZE ( 2 ) VALIDATE ( DIGIT )  : T 
  112 PRINT 
  118  ! GENAUIGKEIT DES ZUGES
  120 F1 = INT ( ( F - 1 ) / 8 ) 
  130 F2 = F - 8 * F1 
  140 T1 = INT ( ( T - 1 ) / 8 ) 
  150 T2 = T - 8 * T1 
  160 IF F1 > 7 OR T1 > 7 THEN 230
  180 IF F2 > 8 OR T2 > 8 THEN 230
  200 IF ABS ( F1 - T1 ) < > 2 OR ABS ( F2 - T2 ) < > 2 THEN 230
  210 IF A ( ( T + F ) / 2 ) = 0 THEN 230
  220 IF A ( F ) = 0 OR A ( T ) = 1 THEN 230
  225 GOTO 250
  230 PRINT "UNGUELTIGER ZUG,VERSUCHE    NOCH MAL..." 
  240 GOTO 100
  245 REM  ERNEUERE SPIELBRETT
  250 A ( T ) = 1 
  260 A ( F ) = 0 
  270 A ( ( T + F ) / 2 ) = 0 
  290 M = M + 1 
  310  ! SPIELBRETTAUSDRUCK
  340 FOR J = 1 TO 57 STEP 8 
  350 FOR I = J TO J + 7 
  360 PRINT A ( I )  ; 
  370 NEXT I 
  380 PRINT 
  390 NEXT J 
  400 PRINT 
  410 GOTO 100
  490  !  SPIELSCHLUSS
  500 S = 0 
  510 FOR I = 1 TO 64 
  520 S = S + A ( I ) 
  530 NEXT I 
  540 PRINT "DU BIST"  ; M  ; "MAL GESPRUNGEN"  : "UND HAST NOCH"  ; S  ; "STEINE"  : "AUF DEM BRETT."  :  : 
  560 INPUT "NOCH EIN VERSUCH?:"  : A$ 
  570 IF A$ = "JA" THEN 70
  575 IF A$ = "NEIN" THEN 600
  580 PRINT "BITTE ANTWORTE MIT 'JA' ODERMIT 'NEIN'."  :: GOTO 560
  600 PRINT  : "FEIN.ICH HOFFE ES HAT DIR   GEFALLEN." 
  999 END 
