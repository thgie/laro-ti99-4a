    1 CALL CLEAR  :: RANDOMIZE  :: PRINT TAB ( 11 )  ; "BAUERN" 
    2 PRINT "    CREATIVE COMPUTING         MORRISTOWN,NEW JERSEY" 
    3 PRINT  :  :  : 
    4  !  BAUERN: DIE ERKLAERUNG   DES BAUERNSPIELS WIRD IN    MARTIN GARDNERS BUCH "THE   UNEXPECTED HANGING AND      OTHER MATHEMATICAL          DIVERSIONS",
    5  !  IM KAPITEL 8 BESCHRIEBEN :EINE LERNMASCHINE FUER EIN STREICHHOLZSCHACHTELSPIEL.  ORGINALE VERSION FUER DAS   H-P TIMESHARE SYSTEM VON
    6  !  R.A.KAAPKE                5/5/76 ANLEITUNGEN VON      JEFF DALTON                 UMWANDLUNG IN MITS-BASIC    VON STEVE NORTH
   10 DIM B ( 19 , 9 ) , M ( 19 , 4 ) , S ( 9 ) , P$ ( 3 ) 
   15 W , L = 0 
   20 DEF FNS ( X ) = - X * ( X = 2 OR X = 5 OR X = 8 ) 
   25 DEF FNR ( X ) = - 3 * ( X = 1 ) - ( X = 3 ) - 4 * ( X = 6 ) - 6 * ( X = 4 ) - 7 * ( X = 9 ) - 9 * ( X = 7 ) + FNS ( X ) 
   30 DEF FNM ( Y ) = Y - INT ( Y / 10 ) * 10 
   35 PP$ = "X.O" 
   40 FOR I = 1 TO 19  :: FOR J = 1 TO 9  :: READ B ( I , J )  :: NEXT J  :: NEXT I 
   45 FOR I = 1 TO 19  :: FOR J = 1 TO 4  :: READ M ( I , J )  :: NEXT J  :: NEXT I 
   50 INPUT "ANWEISUNGEN(J/N)?:"  : A$ 
   70 A$ = SEG$ ( A$ , 1 , 1 ) 
   80 IF A$ = "J" THEN 2000
   90 IF A$ < > "N" THEN 50
  100 X , Y = 0 
  111 S ( 4 ) , S ( 5 ) , S ( 6 ) = 0 
  112 S ( 1 ) , S ( 2 ) , S ( 3 ) = - 1 
  113 S ( 7 ) , S ( 8 ) , S ( 9 ) = 1 
  115 GOSUB 1000
  120 INPUT "DEIN ZUG?:"  : M1 , M2 
  122 IF M1 = INT ( M1 ) AND M2 = INT ( M2 ) AND M1 > 0 AND M1 < 10 AND M2 > 0 AND M2 < 10 THEN 130
  123 PRINT "UNGUELTIGE KOORDINATEN..." 
  124 GOTO 120
  130 IF S ( M1 ) = 1 THEN 150
  140 PRINT "UNGUELTIGER ZUG..."  :: GOTO 120
  150 IF S ( M2 ) = 1 THEN 140
  160 IF M2 - M1 < > - 3 AND S ( M2 ) < > - 1 THEN 140
  170 IF M2 > M1 THEN 140
  180 IF M2 - M1 = - 3 AND S ( M2 ) THEN 140
  185 IF M2 - M1 < - 4 THEN 140
  186 IF M1 = 7 AND M2 = 3 THEN 140
  200 S ( M1 ) = 0  :: S ( M2 ) = 1 
  205 GOSUB 1000
  210 IF S ( 1 ) = 1 OR S ( 2 ) = 1 OR S ( 3 ) = 1 THEN 820
  220 FOR I = 1 TO 9 
  221 IF S ( I ) = - 1 THEN 230
  222 NEXT I 
  223 GOTO 820
  230 FOR I = 1 TO 9 
  240 IF S ( I ) < > - 1 THEN 330
  250 IF S ( I + 3 ) = 0 THEN 350
  260 IF FNR ( I ) = I THEN 320
  270 IF I > 3 THEN 300
  280 IF S ( 5 ) = 1 THEN 350
  290 GOTO 330
  300 IF S ( 8 ) = 1 THEN 350
  310 GOTO 330
  320 IF S ( I + 2 ) = 1 OR S ( I + 4 ) = 1 THEN 350
  330 NEXT I 
  340 GOTO 820
  350 FOR I = 1 TO 19 
  360 FOR J = 1 TO 3 
  370 FOR K = 3 TO 1 STEP - 1 
  380 T ( ( J - 1 ) * 3 + K ) = B ( I , ( J - 1 ) * 3 + 4 - K ) 
  390 NEXT K 
  400 NEXT J 
  410 FOR J = 1 TO 9 
  420 IF S ( J ) < > B ( I , J ) THEN 460
  430 NEXT J 
  440 R = 0 
  450 GOTO 540
  460 FOR J = 1 TO 9 
  470 IF S ( J ) < > T ( J ) THEN 510
  480 NEXT J 
  490 R = 1 
  500 GOTO 540
  510 NEXT I 
  511  ! BEDENKE,DASS DIESE          SCHLEIFE UNENDLICH IST.
  512 PRINT "UNGUELTIGES BRETTMUSTER." 
  530 STOP 
  540 X = I 
  550 FOR I = 1 TO 4 
  560 IF M ( X , I ) THEN 600
  570 NEXT I 
  580 PRINT "ICH GEBE AUF." 
  590 GOTO 820
  600 Y = INT ( RND * 4 + 1 ) 
  601 IF M ( X , Y ) = 0 THEN 600
  610 IF R THEN 630
  620 PRINT "ICH ZIEHE VON "  ; STR$ ( INT ( M ( X , Y ) / 10 ) )  ; " NACH "  ; STR$ ( FNM ( M ( X , Y ) ) ) 
  622 S ( INT ( M ( X , Y ) / 10 ) ) = 0 
  623 S ( FNM ( M ( X , Y ) ) ) = - 1 
  624 GOTO 640
  630 PRINT "ICH ZIEHE VON "  ; STR$ ( FNR ( INT ( M ( X , Y ) / 10 ) ) )  ; " NACH "  ; STR$ ( FNR ( FNM ( M ( X , Y ) ) ) ) 
  632 S ( FNR ( INT ( M ( X , Y ) / 10 ) ) ) = 0 
  633 S ( FNR ( FNM ( M ( X , Y ) ) ) ) = - 1 
  640 GOSUB 1000
  641 IF S ( 7 ) = - 1 OR S ( 8 ) = - 1 OR S ( 9 ) = - 1 THEN 870
  650 FOR I = 1 TO 9 
  660 IF S ( I ) = 1 THEN 690
  670 NEXT I 
  680 GOTO 870
  690 FOR I = 1 TO 9 
  700 IF S ( I ) < > 1 THEN 790
  710 IF S ( I - 3 ) = 0 THEN 120
  720 IF FNR ( I ) = I THEN 780
  730 IF I < 7 THEN 760
  740 IF S ( 5 ) = - 1 THEN 120
  750 GOTO 790
  760 IF S ( 2 ) = - 1 THEN 120
  770 GOTO 790
  780 IF S ( I - 2 ) = - 1 OR S ( I - 4 ) = - 1 THEN 120
  790 NEXT I 
  800 PRINT "DU KANNST NICHT ZIEHEN, ALSO" 
  810 GOTO 870
  820 PRINT "DU GEWINNST." 
  830 M ( X , Y ) = 0 
  840 L = L + 1 
  850 PRINT "ICH HABE"  ; W  ; "UND DU"  ; L  ; "VON INSGESAMMT"  ; L + W  ; "SPIELEN GEWONNEN." 
  851 PRINT 
  860 GOTO 100
  870 PRINT "ICH GEWINNE." 
  880 W = W + 1 
  890 GOTO 850
  900 DATA -1 , -1 , -1 , 1 , 0 , 0 , 0 , 1 , 1 , -1 , -1 , -1 , 0 , 1 , 0 , 1 , 0 , 1 
  905 DATA -1 , 0 , -1 , -1 , 1 , 0 , 0 , 0 , 1 , 0 , -1 , -1 , 1 , -1 , 0 , 0 , 0 , 1 
  910 DATA -1 , 0 , -1 , 1 , 1 , 0 , 0 , 1 , 0 , -1 , -1 , 0 , 1 , 0 , 1 , 0 , 0 , 1 
  915 DATA 0 , -1 , -1 , 0 , -1 , 1 , 1 , 0 , 0 , 0 , -1 , -1 , -1 , 1 , 1 , 1 , 0 , 0 
  920 DATA -1 , 0 , -1 , -1 , 0 , 1 , 0 , 1 , 0 , 0 , -1 , -1 , 0 , 1 , 0 , 0 , 0 , 1 
  925 DATA 0 , -1 , -1 , 0 , 1 , 0 , 1 , 0 , 0 , -1 , 0 , -1 , 1 , 0 , 0 , 0 , 0 , 1 
  930 DATA 0 , 0 , -1 , -1 , -1 , 1 , 0 , 0 , 0 , -1 , 0 , 0 , 1 , 1 , 1 , 0 , 0 , 0 
  935 DATA 0 , -1 , 0 , -1 , 1 , 1 , 0 , 0 , 0 , -1 , 0 , 0 , -1 , -1 , 1 , 0 , 0 , 0 
  940 DATA 0 , 0 , -1 , -1 , 1 , 0 , 0 , 0 , 0 , 0 , -1 , 0 , 1 , -1 , 0 , 0 , 0 , 0 
  945 DATA -1 , 0 , 0 , -1 , 1 , 0 , 0 , 0 , 0 
  950 DATA 24 , 25 , 36 , 0 , 14 , 15 , 36 , 0 , 15 , 35 , 36 , 47 , 36 , 58 , 59 , 0 
  955 DATA 15 , 35 , 36 , 0 , 24 , 25 , 26 , 0 , 26 , 57 , 58 , 0 
  960 DATA 26 , 35 , 0 , 0 , 47 , 48 , 0 , 0 , 35 , 36 , 0 , 0 , 35 , 36 , 0 , 0 
  965 DATA 36 , 0 , 0 , 0 , 47 , 58 , 0 , 0 , 15 , 0 , 0 , 0 
  970 DATA 26 , 47 , 0 , 0 , 47 , 58 , 0 , 0 , 35 , 36 , 47 , 0 , 28 , 58 , 0 , 0 , 15 , 47 , 0 , 0 
 1000 PRINT 
 1010 FOR I = 1 TO 3 
 1015 PRINT TAB ( 10 )  ; 
 1020 FOR J = 1 TO 3 
 1030 PRINT SEG$ ( PP$ , S ( ( I - 1 ) * 3 + J ) + 2 , 1 )  ; 
 1040 NEXT J 
 1060 NEXT I 
 1070 PRINT  :  : 
 1080 RETURN 
 2000 PRINT  :  : "DIESES PROGRAMM SPIELT DAS  BAUERNSPIEL." 
 2010 PRINT "BAUERN WIRD MIT SCHACHBAUERNAUF EINEM 3 MAL 3 BRETT     GESPIELT." 
 2020 PRINT "DIE BAUERN ZIEHEN WIE IM    SCHACHSPIEL - EINE STELLE   VORWAERTS" 
 2030 PRINT "AUF EIN LEERES QUADRAT ODER EINE STELLE VORWAERTS UND" 
 2040 PRINT "DIAGONAL, UM ZU SCHLAGEN.   DEINE BAUERN WERDEN AUF" 
 2050 PRINT "DEM BRETT MIT 'O' UND DIE   DES COMPUTERS MIT 'X'       DARGESTELLT."  :: CALL WARTEN 
 2060 PRINT "EINE LEERE STELLE IST EIN   '.' .UM EINEN ZUG           EINZUGEBEN, TIPPE" 
 2070 PRINT "DIE NUMMER DES QUADRATES,   VON DEM DU KOMMST, GEFOLGT  VON DER" 
 2080 PRINT "NUMMER DES QUADRATEA ZU DEM DU ZIEHST. DIE NUMMERN      MUESSEN " 
 2090 PRINT "DURCH EIN KOMMA GETRENNT    SEIN."  :  :  :: CALL WARTEN 
 2100 PRINT "DER COMPUTER BEGINNT EINE   REIHE VON SPIELEN UND WEISS NUR WANN" 
 2110 PRINT "DAS SPIEL GEWONNEN IST      (UNENTSCHIEDEN IST NICHT     MOEGLICH) UND" 
 2120 PRINT "WIE MAN ZIEHT. ER HAT AM    ANFANG KEINEN STRATEGIEPLAN UND ZIEHT" 
 2130 PRINT "WILLKUERLICH. ER LERNT      JEDOCH MIT DEM SPIEL.       DESHALB WIRD DAS" 
 2140 PRINT "GEWINNEN IMMER SCHWIERIGER. UM DEINEN ANFAENGLICHE      VORTEIL" 
 2150 PRINT "ETWAS AUSZUWAEGEN, WIRD DIR NICHT MITGETEILT, WIE MANN  GEWINNT." 
 2160 PRINT "DU MUSST DIES DURCH SPIELEN LERNEB."  :: CALL WARTEN 
 2165 PRINT "DAS SPIELBRETT WIRD WIE     FOLGT NUMMERIERT:" 
 2170 PRINT  : TAB ( 10 )  ; "123"  : TAB ( 10 )  ; "456"  : TAB ( 10 )  ; "789"  :  : 
 2180 PRINT "UM Z.B. DEN GANZ RECHTEN    BAUERN VORWAERTS ZU BEWEGEN,MUSST" 
 2190 PRINT "DU AUF DIE FRAGE            'DEIN ZUG?:' MIT 9,6         ANTWORTEN. DA ICH FAIR BIN," 
 2200 PRINT "DARFST DU IMMER ANFANGEN."  :  :: CALL WARTEN 
 2220 GOTO 100
 2500 SUB WARTEN 
 2510 CALL KEY ( 0 , K , S ) 
 2520 IF K < > 13 THEN 2510
 2530 SUBEND 
