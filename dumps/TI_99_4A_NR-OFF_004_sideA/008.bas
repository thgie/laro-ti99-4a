32726 SUB TITEL ( A$ , W1 , S1 , W2 , S2 , F1 , F2 , PA ) 
32727 IF LEN ( A$ ) > 28 THEN CALL SOUND ( 100 , 220 , 4 )  :: PRINT "* TITEL STRING ZU LANG"  :: SUBEXIT 
32728 IF W1 < 1 OR W1 > 23 OR W2 < 1 OR W2 > 23 OR S1 < 1 OR S1 > 31 OR F1 < 1 OR F1 > 16 OR F2 < 1 OR F2 > 16 OR PA > 10 THEN CALL SOUND ( 200 , 220 , 4 )  :: PRINT "* FALSCHER WERT IN TITEL"  :: SUBEXIT 
32729 IF S2 < 1 OR S2 > 31 THEN CALL SOUND ( 200 , 220 , 4 )  :: PRINT "* FALSCHER WERT IN TITEL" 
32730 CALL CLEAR  :: CALL SCREEN ( F2 )  :: FOR I = 1 TO 14  :: CALL COLOR ( I , F1 , F2 )  :: NEXT I  ! TITELPROGRAMM
32731 DIM K$ ( 112 ) 
32732 FOR F = 32 TO 127  :: CALL CHARPAT ( F , K$ ( F - 31 ) )  :: NEXT F 
32733 W = W1  :: S = S1  :: X = 33 
32734 CALL CLEAR 
32735 FOR I = 1 TO LEN ( A$ ) 
32736 A1$ = SEG$ ( A$ , I , 1 )  :: A1$ = K$ ( ASC ( A1$ ) - 31 ) 
32737 FOR J = 1 TO LEN ( A1$ ) 
32738 A2$ = SEG$ ( A1$ , J , 1 ) 
32739 IF J / 2 = INT ( J / 2 ) THEN F = 1 ELSE F = 2 
32740 IF A2$ = "0" THEN X$ = "0000"  :: GOTO 32756
32741 IF A2$ = "1" THEN X$ = "0303"  :: GOTO 32756
32742 IF A2$ = "2" THEN X$ = "0C0C"  :: GOTO 32756
32743 IF A2$ = "3" THEN X$ = "0F0F"  :: GOTO 32756
32744 IF A2$ = "4" THEN X$ = "3030"  :: GOTO 32756
32745 IF A2$ = "5" THEN X$ = "3333"  :: GOTO 32756
32746 IF A2$ = "6" THEN X$ = "3C3C"  :: GOTO 32756
32747 IF A2$ = "7" THEN X$ = "3F3F"  :: GOTO 32756
32748 IF A2$ = "8" THEN X$ = "C0C0"  :: GOTO 32756
32749 IF A2$ = "9" THEN X$ = "C3C3"  :: GOTO 32756
32750 IF A2$ = "A" THEN X$ = "CCCC"  :: GOTO 32756
32751 IF A2$ = "B" THEN X$ = "CFCF"  :: GOTO 32756
32752 IF A2$ = "C" THEN X$ = "F0F0"  :: GOTO 32756
32753 IF A2$ = "D" THEN X$ = "F3F3"  :: GOTO 32756
32754 IF A2$ = "E" THEN X$ = "FCFC"  :: GOTO 32756
32755 IF A2$ = "F" THEN X$ = "FFFF" 
32756 B$ ( F ) = B$ ( F ) & X$ 
32757 NEXT J  :: X$ = "" 
32758 CALL CHAR ( X , B$ ( 2 ) & B$ ( 1 ) ) 
32759 B$ ( 1 ) , B$ ( 2 ) = "" 
32760 CALL HCHAR ( W , S , X )  :: CALL HCHAR ( W + 1 , S , X + 1 )  :: CALL HCHAR ( W , S + 1 , X + 2 )  :: CALL HCHAR ( W + 1 , S + 1 , X + 3 ) 
32761 X = X + 4  :: IF X > 143 THEN GOTO 32764
32762 S = S + 2  :: IF S > S2 THEN W = W + 2  :: S = S1  :: IF W > W2 THEN 32764
32763 NEXT I 
32764 FOR I = 1 TO PA * 1000  :: NEXT I  :: CALL CHARSET 
32765 SUBEND 
