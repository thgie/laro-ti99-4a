   10  ! ************************
   20  ! *                      *
   30  ! * TM 9900 DISASSEMBLER *
   40  ! *                      *
   50  ! * WRITTEN BY M.FRANZ   *
   60  ! *                      *
   70  ! * 15.07.1983 REV.2/83  *
   80  ! *                      *
   90  ! ************************
  100 DIM WORT$ ( 76 ) 
  110 FOR N = 0 TO 76 
  120 READ WORT$ ( N ) 
  130 NEXT N 
  140 DATA DATA , LI , AI , ANDI , ARI , CI , STWP , STST , LWPI , LIMI , DATA , IDLE , RSET , RTWP , CKON 
  150 DATA CKOF , LREX , BLWP , B , X , CLR , NEG , INV , INC , INCT , DEC , DECT , BL , SWPB , SETO , ABS , DATA 
  160 DATA DATA , SRA , SRL , SLA , SRC , DATA , DATA , DATA , DATA , JMP , JLT , JLE , JEQ , JHE , JGT , JNE , JNC , JOC , JNO , JL , JH , JOP 
  170 DATA SBO , SBZ , TB , COC , CZC , XOR , XOP , LDCR , STCR , MPY , DIV , SZC , SCZB , S , SB , C , CB , A , AB , MOV , MOVB , SOC , SOCB , 
  180 CALL CLEAR 
  190 FLAG = 0  :: INPUT "VON,BIS "  : ANFANG$ , ENDE$ 
  200 INPUT "FILENAME?:"  : FILE$ 
  210 CALL HEXDEZ ( ANFANG , ANFANG$ )  :: CALL HEXDEZ ( ENDE , ENDE$ ) 
  220 ADRESSE = ANFANG 
  230 IF FILE$ < > "" THEN DATEI = 1  :: OPEN # DATEI  : FILE$ , OUTPUT ELSE DATEI = 0 
  240 ADRESSE1 = ADRESSE  :: IF ADRESSE1 > 32767 THEN ADRESSE1 = - 65536 + ADRESSE 
  250 CALL PEEK ( ADRESSE1 , WERT1 , WERT2 ) 
  260 WERT = WERT1 * 256 + WERT2 
  270 CALL DEZHEX ( WERT , WERT$ ) 
  280 CALL DEZHEX ( ADRESSE , ADRESSE$ ) 
  290 A = WERT  :: IF A < 512 THEN A = 511 
  300 IF A < = 1024 THEN H = INT ( A / 32 ) - 15 ELSE IF A < = 2048 THEN H = INT ( A / 64 ) + 1 ELSE IF A < = 8192 THEN H = INT ( A / 256 ) + 25 ELSE 320
  310 GOTO 330
  320 IF A < = 16384 THEN H = INT ( A / 1024 ) + 49 ELSE H = INT ( A / 4096 ) + 61 
  330 PRINT # DATEI  : ADRESSE$  ; " "  ; WERT$  ; " "  ;  :: IF FLAG THEN PRINT # DATEI  :: FLAG = FLAG - 1  :: GOTO 420ELSE PRINT # DATEI  : WORT$ ( H )  ; TAB ( 16 )  ; 
  340 IF H > 0 AND H < 10 THEN 450
  350 IF H > 10 AND H < 16 THEN 540
  360 IF H > 15 AND H < 31 THEN 580
  370 IF H > 32 AND H < 37 THEN 640
  380 IF H > 40 AND H < 57 THEN 790
  390 IF H > 56 AND H < 65 THEN 700
  400 IF H > 64 THEN 870
  410 PRINT # DATEI  : TAB ( 16 )  ; ">"  ; WERT$ 
  420 CALL KEY ( 0 , K , S )  :: IF S = 0 THEN 430ELSE IF K = 155 THEN 440ELSE 420
  430 IF ADRESSE < ENDE THEN ADRESSE = ADRESSE + 2  :: GOTO 240
  440 IF DATEI THEN CLOSE # DATEI  :: GOTO 190ELSE 190
  450  ! FORMAT 8 BEFEHLE
  460  ! 
  470 IF A > = 736 THEN 510
  480 REGNR = A - INT ( A / 16 ) * 16 
  490 IF A > = 672 AND A > 736 THEN PRINT # DATEI  : "R"  ; STR$ ( REGNR )  :: GOTO 530
  500 PRINT # DATEI  : "R"  ; STR$ ( REGNR )  ; ","  ; 
  510 OFS = 2  :: GOSUB 990 ! PRINT DATA
  520 PRINT # DATEI 
  530 GOTO 420
  540  ! FORMAT 7 BEFEHLE
  550  ! 
  560 PRINT # DATEI 
  570 GOTO 420
  580  ! FORMAT 6 BEFEHLE
  590  ! 
  600 REGNR = A - INT ( A / 16 ) * 16 
  610 TSFELD = ( A - INT ( A / 64 ) * 64 - REGNR ) / 16 
  620 OFS = 2  :: GOSUB 1050 ! PRINT REGISTER
  630 PRINT # DATEI  :: GOTO 420
  640  ! FORMAT 5 BEFEHLE
  650  ! 
  660 REGNR = A - INT ( A / 16 ) * 16 
  670 COUNT = ( A - INT ( A / 256 ) * 256 - REGNR ) / 16 
  680 PRINT # DATEI  : "R"  ; STR$ ( REGNR )  ; ","  ; STR$ ( COUNT ) 
  690 GOTO 420
  700  ! FORMAT 3 BEFEHLE
  710  ! 
  720 REGNR = A - INT ( A / 16 ) * 16 
  730 TSFELD = ( A - INT ( A / 64 ) * 64 - REGNR ) / 16 
  740 OFS = 2  :: GOSUB 1050 ! PRINT REGISTER
  750 REGNR = ( ( A - INT ( A / 1024 ) * 1024 ) - REGNR - ( TSFELD * 16 ) ) / 64 
  760 PRINT # DATEI  : ","  ;  :: IF A < 11264 OR A > 14336 THEN PRINT # DATEI  : "R"  ; 
  770 PRINT # DATEI  : STR$ ( REGNR ) 
  780 GOTO 420
  790  ! FORMAT 2 BEFEHLE
  800  ! 
  810 SIDIS = A - INT ( A / 256 ) * 256 
  820 IF A > = 7424 THEN PRINT # DATEI  : STR$ ( SIDIS )  :: GOTO 860
  830 IF SIDIS > 127 THEN SIDIS = - 256 + SIDIS 
  840 CALL DEZHEX ( SIDIS * 2 + ADRESSE + 2 , SIDIS$ ) 
  850 PRINT # DATEI  : ">"  ; SIDIS$ 
  860 GOTO 420
  870  ! FORMAT 1 BEFEHL
  880  ! 
  890 REGNR = A - INT ( A / 16 ) * 16 
  900 TSFELD = INT ( ( A - INT ( A / 64 ) * 64 ) / 16 ) 
  910 OFS = 2  :: GOSUB 1050 ! PRINT REGISTER
  920 IF TSFELD = 2 THEN OFS = 4 
  930 REGNR = INT ( ( A - INT ( A / 1024 ) * 1024 ) / 64 ) 
  940 TSFELD = INT ( ( A - INT ( A / 4096 ) * 4096 ) / 1024 ) 
  950 PRINT # DATEI  : ","  ; 
  960 GOSUB 1050 ! PRINT REGISTER
  970 PRINT # DATEI 
  980 GOTO 420
  990  ! PRINT DATA
 1000 ADRESSE1 = ADRESSE + OFS  :: IF ADRESSE1 > 32767 THEN ADRESSE1 = - 65536 + ADRESSE + OFS 
 1010 CALL PEEK ( ADRESSE1 , WERT1 , WERT2 ) 
 1020 CALL DEZHEX ( WERT1 * 256 + WERT2 , WERT$ ) 
 1030 PRINT # DATEI  : ">"  ; WERT$  ; 
 1040 FLAG = FLAG + 1  :: RETURN 
 1050  ! PRINT REGISTER
 1060 IF TSFELD = 0 THEN PRINT # DATEI  : "R"  ; STR$ ( REGNR )  ;  :: GOTO 1110
 1070 IF TSFELD = 1 THEN PRINT # DATEI  : "*R"  ; STR$ ( REGNR )  ;  :: GOTO 1110
 1080 IF TSFELD = 2 THEN PRINT # DATEI  : "*R"  ; STR$ ( REGNR )  ; "+"  ;  :: GOTO 1110
 1090 PRINT # DATEI  : "@"  ;  :: GOSUB 990 ! PRINT DATA
 1100 IF REGNR < > 0 THEN PRINT # DATEI  : "(R"  ; STR$ ( REGNR )  ; ")"  ; 
 1110 RETURN 
 1120 SUB HEXDEZ ( DEZ , HEX$ ) 
 1130 FELD ( 1 ) = 4096 
 1140 FELD ( 2 ) = 256 
 1150 FELD ( 3 ) = 16 
 1160 FELD ( 4 ) = 1 
 1170 HEXZ$ = "0123456789ABCDEF" 
 1180 DEZ = 0  :: HEX$ = "0000" & HEX$  :: HEX$ = SEG$ ( HEX$ , LEN ( HEX$ ) - 3 , 4 ) 
 1190 FOR N = 1 TO 4 
 1200 DEZ = ( POS ( HEXZ$ , SEG$ ( HEX$ , N , 1 ) , 1 ) - 1 ) * FELD ( N ) + DEZ 
 1210 NEXT N 
 1220 SUBEND 
 1230 SUB DEZHEX ( DEZ , HEX$ ) 
 1240 HEXZ$ = "0123456789ABCDEF" 
 1250 HEX$ , HEX1$ = "" 
 1260 DEZ1 = DEZ 
 1270 FOR N = 1 TO 4 
 1280 REST = ( DEZ1 - INT ( DEZ1 / 16 ) * 16 ) 
 1290 DEZ1 = INT ( DEZ1 / 16 ) 
 1300 HEX1$ = HEX1$ & SEG$ ( HEXZ$ , REST + 1 , 1 ) 
 1310 NEXT N 
 1320 FOR N = 1 TO 4 
 1330 HEX$ = HEX$ & SEG$ ( HEX1$ , 5 - N , 1 ) 
 1340 NEXT N 
 1350 SUBEND 
 1360 SUB PEEK ( A , W1 , W2 ) 
 1370 RANDOMIZE A 
 1380 W2 = INT ( 255 * RND ) 
 1390 W1 = INT ( 255 * RND ) 
 1400 SUBEND 
