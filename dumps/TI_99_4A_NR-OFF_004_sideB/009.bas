  100 REM  LANDING
  110 CALL MAGNIFY ( 4 ) 
  120 ON WARNING NEXT 
  130 CALL CLEAR 
  140 CALL SCREEN ( 2 ) 
  150 FOR X = 0 TO 8  :: CALL COLOR ( X , 2 , 12 )  :: NEXT X 
  160 CALL COLOR ( 9 , 2 , 2 , 10 , 15 , 15 ) 
  170 CALL VCHAR ( 1 , 17 , 96 , 384 ) 
  180 CALL CHAR ( 96 , "0F1F3939393F1F3F3F3F372F40800000E0F0383838F8F0F8F8F8D8E80402" ) 
  190 CALL CHAR ( 108 , "0F0F0F070B03010802" , 110 , "E0E0C0C0A08020804" ) 
  200 RESTORE 
  210 CALL SPRITE ( # 1 , 96 , 8 , 1 , 184 ) 
  220 CALL ACC ( 2 , "STUFE?(-15):" , S ) 
  230 IF S < 1 OR S > 15 THEN 220
  240 E = 500 + ( S - 1 ) * 20 
  250 G , T = 0 
  260 B = 200 - ( S - 1 ) * 5 
  270 READ A , A$  :: IF A THEN CALL DIS ( A , A$ )  :: GOTO 270
  280  ! SPIEL
  290 IF E < 1000 THEN 350
  300 CALL HCHAR ( 17 , 1 , 32 , 256 ) 
  310 DISPLAY AT ( 18 , 1 )  : "DU HAST DICH SOEBEN IN DEN  WELTRAUM HINAUSKATAPULTIERT." 
  320 CALL DELSPRITE ( ALL ) 
  330 DISPLAY AT ( 22 , 1 ) BEEP  : "AUF WIEDERSEHEN." 
  340 FOR I = 1 TO 1000  :: NEXT I  :: CALL HCHAR ( 22 , 1 , 32 , 32 )  :: GOTO 830
  350 IF E > 0 THEN 420
  360 IF G < 3 AND E > - 1 THEN 690
  370 CALL DELSPRITE ( ALL ) 
  380 CALL HCHAR ( 17 , 1 , 32 , 256 ) 
  390 CALL SOUND ( 2000 , - 7 , 5 ) 
  400 DISPLAY AT ( 18 , 1 )  : "GRATULIERE!"  : "DU BIST SOEBEN AUF DEM MOND EINGESCHLAGEN UND HAST EINENNEUNEN MONDKRATER VON"  : STR$ ( ABS ( INT ( G * E / 20 ) ) + 1 ) & " M DURCHMESSER GEFORMT." 
  410 FOR I = 1 TO 4000  :: NEXT I  :: CALL HCHAR ( 18 , 1 , 32 , 224 )  :: GOTO 830
  420 CALL DIS ( 10 , "HOEHE=" & STR$ ( E ) ) 
  430 CALL DIS ( 12 , "GESCHW.=" & STR$ ( G + 10 ) ) 
  440 CALL DIS ( 14 , "ENERGIE=" & STR$ ( B ) ) 
  450 CALL DIS ( 16 , "ZEIT=" & STR$ ( T ) ) 
  460 IF E - G < 10 AND G > = 8 THEN CALL DIS ( 20 , "ABBREMSEN!" ) 
  470 IF B < = 0 THEN B1 = 0  :: GOTO 510
  480 CALL ACC ( 6 , "BRENNRATE?:" , B1 ) 
  490 IF B1 > B THEN 480
  500 CALL DIS ( 20 , "" ) 
  510 B = B - B1 
  520 T = T + 1 
  530 G = G + 10 - B1 
  540 E = E - G 
  550 CALL POSITION ( # 1 , P , Q ) 
  560 IF P < 20 AND G < 0 THEN 640
  570 IF P < 130 THEN 610
  580 CALL LOCATE ( # 1 , 40 , 184 ) 
  590 CALL DELSPRITE ( # 2 ) 
  600 CALL MOTION ( # 1 , 0 , 0 , # 2 , 0 , 0 )  :: GOTO 550
  610 IF B1 = 0 THEN CALL DELSPRITE ( # 2 )  :: GOTO 630
  620 CALL SPRITE ( # 2 , 108 , 12 , P + 22 , Q ) 
  630 CALL MOTION ( # 1 , SGN ( G ) * 10 , 0 , # 2 , SGN ( G ) * 10 , 0 ) 
  640 IF B1 THEN CALL SOUND ( 400 , - 6 , 10 ) 
  650 FOR I = 1 TO 100  :: NEXT I 
  660 CALL MOTION ( # 1 , 0 , 0 , # 2 , 0 , 0 ) 
  670 GOTO 280
  680 GOTO 680
  690 CALL LOCATE ( # 1 , 10 , 184 , # 2 , 32 , 184 )  :: FOR I = 17 TO 32  :: CALL VCHAR ( 18 , I , 108 , 7 )  :: NEXT I 
  700 CALL MOTION ( # 1 , 6 , 0 , # 2 , 6 , 0 ) 
  710 CALL SOUND ( 4250 , - 6 , 5 ) 
  720 CALL COINC ( # 1 , 110 , 184 , 3 , O )  :: IF NOT O THEN 720
  730 CALL MOTION ( # 1 , 0 , 0 , # 2 , 0 , 0 ) 
  740 CALL VCHAR ( 1 , 1 , 32 , 384 ) 
  750 CALL DELSPRITE ( # 2 )  :: CALL SOUND ( - 1500 , - 1 , 10 ) 
  760 CALL LOCATE ( # 1 , 110 , 184 ) 
  770 CALL DIS ( 10 , "GRATULIERE!" ) 
  780 CALL DIS ( 12 , "MISSION WAR" ) 
  790 CALL DIS ( 14 , "ERFOLGREICH!" ) 
  800 CALL DIS ( 16 , "DEINE ERFOLGS-" ) 
  810 CALL DIS ( 18 , "BEWERTUNG IST" ) 
  820 CALL DIS ( 20 , STR$ ( INT ( B * 100 / ABS ( G ) ) ) & "!" ) 
  830 CALL DIS ( 22 , "NOCH EIN SPIEL" )  :: CALL DIS ( 23 , "?:J" )  :: ACCEPT AT ( 23 , 3 ) SIZE ( - 1 ) BEEP VALIDATE ( "JN" )  : A$ 
  840 IF A$ = "J" THEN 100
  850 GOTO 850
  860 DATA 2 , "   LANDING" , 3 , "   =======" , 0 , 0 
  870 SUB ACC ( S , A$ , X )  :: DISPLAY AT ( S , 1 ) SIZE ( 14 )  : A$ 
  880 ACCEPT AT ( S , LEN ( A$ ) + 1 ) BEEP SIZE ( 14 - LEN ( A$ ) ) VALIDATE ( DIGIT )  : X  :: SUBEND 
  890 SUB DIS ( S , A$ )  :: DISPLAY AT ( S , 1 ) SIZE ( 14 )  : A$  :: SUBEND 
