   10 REM *********************
   15 REM * TI BENUTZER-CLUB  *
   20 REM *   SCHWEIZ (TIC)   *
   25 REM *      ZUERICH      *
   30 REM * ----------------- *
   35 REM *                   *
   40 REM *  TASTATURTRAINER  *
   45 REM *                   *
   50 REM * ----------------- *
   55 REM * VON ANGELO BERTI  *
   60 REM *     (MAI 1983)    *
   65 REM *********************
   70 REM 
   75 REM    >> TI-BASIC <<
   80 REM 
   85 REM  SPEICHERBEDARF:
   90 REM  CA. 8700 BYTES
   92 REM 
   95 REM  GRUNDAUSRUESTUNG
   97 REM 
  500 REM  ** HAUTPRGRAMM **
  510 REM 
  520 DIM CHAR ( 23 , 30 ) 
  530 RANDOMIZE 
  535 CALL SCREEN ( 4 ) 
  540 CALL CLEAR 
  550 FOR J = 9 TO 12 
  560 CALL COLOR ( J , 2 , 14 ) 
  570 NEXT J 
  580 FOR J = 2 TO 8 
  590 CALL COLOR ( J , 2 , 15 ) 
  600 NEXT J 
  610 IF R = 82 THEN 640
  620 RESTORE 
  630 CALL CLEAR 
  640 PRINT "     TASTATUR-TRAINER" 
  650 FOR I = 1 TO 18 
  660 PRINT 
  670 NEXT I 
  680 REM 
  690 CHARCNT = 0 
  700 ZEIT = 0 
  710 REM 
  720 FOR ZEILE = 11 TO 23 STEP 3 
  730 FOR SPALTE = 6 TO 30 STEP 2 
  740 READ CHAR ( ZEILE , SPALTE ) 
  750 IF CHAR ( ZEILE , SPALTE ) = 0 THEN 820
  760 IF CHAR ( ZEILE , SPALTE ) = - 1 THEN 830
  770 IF CHAR ( ZEILE , SPALTE ) = 32 THEN 800
  780 CALL HCHAR ( ZEILE , SPALTE , CHAR ( ZEILE , SPALTE ) ) 
  790 GOTO 810
  800 PRINT " "  ; 
  810 NEXT SPALTE 
  820 NEXT ZEILE 
  830 PRINT 
  840 PRINT "DRUECKE EINE TASTE ZUM START" 
  850 CALL KEY ( 3 , S , STATUS ) 
  860 IF STATUS = 0 THEN 850
  870 CALL HCHAR ( 23 , 1 , 32 , 30 ) 
  880 REM 
  890 N = INT ( RND * 47 ) + 44 
  900 IF ( N > = 60 ) * ( N < = 64 ) + ( N = 45 ) + ( N = 58 ) + ( N = ALTCHAR ) THEN 890
  910 ALTCHAR = N 
  920 CALL VCHAR ( 7 , 16 , N ) 
  930 REM 
  940 ZEIT = ZEIT + 1 
  950 IF ZEIT > 900 THEN 1040
  960 CALL KEY ( 0 , CR , STATUS ) 
  970 IF STATUS = 0 THEN 940
  980 CALL SOUND ( 20 , 880 , 10 ) 
  990 CHARCNT = CHARCNT + 1 
 1000 REM 
 1010 ZEIT = ZEIT + 12 
 1020 GOTO 1110
 1030 PRINT TAB ( 4 )  ; 
 1040 PRINT "ZEICHEN/MINUTE= "  ; CHARCNT 
 1050 PRINT 
 1060 PRINT "DRUECKE R FUER NOCHMALS"  ; 
 1070 CALL KEY ( 0 , R , STATUS ) 
 1080 IF STATUS < = 0 THEN 1070
 1090 IF R = ASC ( "R" ) THEN 620
 1100 STOP 
 1110 IF CR < > N THEN 1230
 1120 FOR ZEILE = 11 TO 23 STEP 3 
 1130 FOR SPALTE = 6 TO 30 STEP 2 
 1140 IF CHAR ( ZEILE , SPALTE ) = N THEN 1170
 1150 NEXT SPALTE 
 1160 NEXT ZEILE 
 1170 CALL HCHAR ( ZEILE - 2 , SPALTE , N ) 
 1180 CALL HCHAR ( ZEILE - 2 , SPALTE , 32 ) 
 1190 FOR VERZOEG = 1 TO 200 
 1200 NEXT VERZOEG 
 1210 CALL HCHAR ( ZEILE - 2 , SPALTE , N ) 
 1220 GOTO 890
 1230 CHARCNT = CHARCNT - 1 
 1240 CALL SOUND ( 50 , 110 , 9 ) 
 1250 GOTO 890
 1260 REM 
 1270 DATA 49 , 50 , 51 , 52 , 53 , 54 , 55 , 56 , 57 , 48 , 61 , 0 
 1280 DATA 81 , 87 , 69 , 82 , 84 , 89 , 85 , 73 , 79 , 80 , 47 , 0 
 1290 DATA 65 , 83 , 68 , 70 , 71 , 72 , 74 , 75 , 76 , 59 , 0 
 1300 DATA 32 , 90 , 88 , 67 , 86 , 66 , 78 , 77 , 44 , 46 , -1 
 1310 END 
