   10 REM ********************
   15 REM * TI BENUTZER-CLUB *
   20 REM *   SCHWEIZ (TIC)  *
   25 REM * ---------------- *
   30 REM *  DAS GEHEIMNIS   *
   35 REM *  DER DREI RINGE  *
   40 REM * ---------------- *
   45 REM *  UEBERARBEITET   *
   50 REM * VON ANGELO BERTI *
   55 REM *    (NOV.1983)    *
   60 REM ********************
   65 REM 
   70 REM ** SPEICHERBEDARF:
   75 REM     CA. 3250 BYTES **
   80 REM 
   85 REM  GRUNDAUSSTATTUNG
   90 REM 
   95 REM 
  500 REM ** HAUPTPROGRAMM **
  510 REM 
  520 CALL CLEAR 
  530 CALL SCREEN ( 6 ) 
  540 FOR X = 1 TO 14 
  550 CALL COLOR ( X , 16 , 6 ) 
  560 NEXT X 
  570 PRINT "       DAS GEHEIMNIS"  :  : "      DER DREI TUERME"  : "      ***************" 
  580 PRINT  :  : "DAS ZIEL DIESES SPIELS"  :  : "IST ES, DIE VIER RINGE VON"  :  : "DER LINKEN NADEL <1> ZUR" 
  590 PRINT  : "RECHTEN NADEL <3> ZU BRINGEN"  :  : "IMMER NUR EINEN RING AUF'S"  :  : "MAL. ES MUSS VERMIEDEN" 
  600 PRINT  : "WERDEN, DASS GROSSE RINGE"  :  : "AUF KLEINERE RINGE GESETZT"  :  : "WERDEN." 
  610 FOR VERZOEG = 1 TO 5000 
  620 NEXT VERZOEG 
  630 CALL CLEAR 
  640 PRINT TAB ( 10 )  ; "TASTEN" 
  650 PRINT TAB ( 10 )  ; "------" 
  660 PRINT  : "-1- NADEL 1" 
  670 PRINT  : "-2- NADEL 2" 
  680 PRINT  : "-3- NADEL 3" 
  690 PRINT  : "BEISPIEL:" 
  700 PRINT "---------" 
  710 PRINT "UM ALSO EINEN RING VON NADEL"  : "<1> NACH <3> ZU BRINGEN," 
  720 PRINT "WIRD EINFACH ZUERST DIE <1>"  : "DANN DIE <3> GEDRUECKT." 
  730 PRINT "DASSELBE GILT VON <3> NACH"  : "<2>:"  : "ZUERST DIE <3> DANN DIE <2>." 
  740 PRINT  : "ENTER ZUM START" 
  750 CALL KEY ( 0 , K , S ) 
  760 IF S = 0 THEN 750
  770 IF K < > 13 THEN 750
  780 CALL CLEAR 
  790 CALL SOUND ( 50 , 110 , 15 ) 
  800 DIM PEG ( 3 ) , TOP ( 3 ) 
  810 CALL COLOR ( 7 , 1 , 1 ) 
  820 CALL COLOR ( 8 , 2 , 2 ) 
  830 PEG ( 1 ) = 1.357 
  840 PEG ( 2 ) = 0 
  850 PEG ( 3 ) = 0 
  860 TOP ( 1 ) = 10 
  870 TOP ( 2 ) = 14 
  880 TOP ( 3 ) = 14 
  890 CALL CLEAR 
  900 CALL HCHAR ( 10 , 6 , 88 ) 
  910 CALL HCHAR ( 11 , 5 , 88 , 3 ) 
  920 CALL HCHAR ( 12 , 4 , 88 , 5 ) 
  930 CALL HCHAR ( 13 , 3 , 88 , 7 ) 
  940 CALL KEY ( 3 , FROM , STATUS ) 
  950 IF STATUS = 0 THEN 940
  960 CALL KEY ( 3 , DUMMY , STATUS ) 
  970 IF STATUS = - 1 THEN 960
  980 FROM = FROM - 48 
  990 CALL SOUND ( 100 , 110 , 18 ) 
 1000 CALL KEY ( 3 , TOO , STATUS ) 
 1010 IF STATUS = 0 THEN 1000
 1020 CALL KEY ( 3 , DUMMY , STATUS ) 
 1030 IF STATUS = - 1 THEN 1020
 1040 TOO = TOO - 48 
 1050 CALL SOUND ( 100 , 262 , 17 ) 
 1060 IF ( FROM < 1 ) + ( FROM > 3 ) + ( TOO > 3 ) + ( TOO < 1 ) THEN 940
 1070 IF ( PEG ( FROM ) = 0 ) + ( ( PEG ( TOO ) < > 0 ) * ( PEG ( FROM ) > PEG ( TOO ) ) ) THEN 940
 1080 SIZ@ = INT ( PEG ( FROM ) ) 
 1090 TOP ( TOO ) = TOP ( TOO ) - 1 
 1100 CALL HCHAR ( TOP ( FROM ) , 3 + ( FROM - 1 ) * 9 + .5 * ( 7 - SIZ@ ) , 87 , SIZ@ ) 
 1110 TOP ( FROM ) = TOP ( FROM ) + 1 
 1120 CALL HCHAR ( TOP ( TOO ) , 3 + ( TOO - 1 ) * 9 + .5 * ( 7 - SIZ@ ) , 88 , SIZ@ ) 
 1130 PEG ( FROM ) = 10 * ( PEG ( FROM ) - SIZ@ ) 
 1140 PEG ( TOO ) = .1 * PEG ( TOO ) + SIZ@ 
 1150 IF PEG ( 3 ) = 1.357 THEN 1170
 1160 GOTO 940
 1170 CALL SOUND ( - 100 , 110 , 10 , 220 , 10 ) 
 1180 CALL SOUND ( - 100 , 330 , 10 , 440 , 10 ) 
 1190 CALL SOUND ( - 100 , 550 , 10 , 660 , 10 ) 
 1200 CALL SOUND ( - 100 , 770 , 10 , 880 , 10 ) 
 1210 CALL CLEAR 
 1220 CALL SCREEN ( 16 ) 
 1230 FOR X = 1 TO 14 
 1240 CALL COLOR ( X , 2 , 1 ) 
 1250 NEXT X 
 1260 PRINT TAB ( 8 )  ; "S I E G !"  :  :  :  :  :  :  :  :  :  :  : 
 1270 PRINT  :  : "NEUES SPIEL? (J/N)" 
 1280 CALL KEY ( 0 , K , S ) 
 1290 IF S = 0 THEN 1280
 1300 IF K = 74 THEN 800
 1310 END 
