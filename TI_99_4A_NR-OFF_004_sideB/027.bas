  100 CALL CLEAR 
  110 FOR I = 0 TO 11  :: CALL COLOR ( I , 2 , 8 )  :: NEXT I 
  120 CALL SCREEN ( 5 ) 
  130 CALL CHAR ( 112 , "000000000000FFFFC0C0C0C0C0C0C0C00303030303030303FFFF" ) 
  140 CALL CHAR ( 96 , "FFFFFFFFFFFFFFFF" ) 
  150 CALL CHAR ( 42 , "00387CFEFEFE7C38" , 64 , "003844BAA2BA4438" ) 
  160 DISPLAY AT ( 5 , 8 )  : "ppppppppppppp"  : "       q           r"  : "       q BARRIKADE r"  : "       q           r"  : "       sssssssssssss" 
  170 DISPLAY AT ( 15 , 6 )  : "@ 1983 BY L. SACER"  :  :  :  :  : " IRGENDEINE TASTE DRUECKEN" 
  180 CALL KEY ( 0 , K , S )  :: IF S < = 0 THEN 180
  190 CALL CLEAR 
  200 DISPLAY AT ( 8 , 5 )  : "BITTE WAEHLEN SIE:"  :  : "     1 ERKLAERUNG"  :  : "     2 NEUES SPIEL"  :  : "     3 ENDE" 
  210 CALL KEY ( 0 , K , S )  :: IF K < 49 OR K > 51 THEN 210
  220 ON K - 48 GOTO 225, 270, 720
  225 CALL CLEAR 
  230 DISPLAY AT ( 2 , 1 )  : "SIE SIND IM GEFAENGNIS,"  : "UND SIE WOLLEN AUSBRECHEN."  : "SIE STEHEN VOR DER"  : "GEFAENGNISMAUER UND VER-" 
  240 DISPLAY AT ( 6 , 1 )  : "SUCHEN DIESE MIT EINER"  : "EISENKUGEL ZU ZERSCHLAGEN.  JEDER ZERBROCHENE STEIN GIBTEINEN PUNKT." 
  250 DISPLAY AT ( 11 , 1 )  : "ABER ACHTUNG !!"  : "WENN IHNEN DIE KUGEL ZU"  : "BODEN FAELLT GIBT'S"  : "MINUSPUNKTE."  :  : "DAS ZIEL IST ALLE STEINE ZU ZERSCHLAGEN." 
  255 DISPLAY AT ( 18 , 1 )  : "WENN SIE AUFHOEREN WOLLEN   ZU SPIELEN DANN DRUECKEN SIE[FCTN 9](BACK)."  :  :  : " IRGENDEINE TASTE DRUECKEN" 
  260 CALL KEY ( 0 , K , S )  :: IF S < = 0 THEN 260
  270 CALL CLEAR 
  280 CALL SCREEN ( 12 ) 
  290 CALL COLOR ( 4 , 2 , 11 , 3 , 2 , 11 , 1 , 14 , 8 , 2 , 2 , 8 , 0 , 13 , 8 , 9 , 5 , 8 , 10 , 11 , 11 ) 
  300 X , Y = - 1 
  310 A1 , A = 23 
  320 B1 , B = 16 
  330 V , V1 = 16 
  340 P , TR = 0 
  350 CALL HCHAR ( 8 , 1 , 30 , 256 ) 
  360 CALL VCHAR ( 1 , 1 , 140 , 24 ) 
  370 FOR I = 1 TO 3  :: CALL HCHAR ( I , 2 , 104 , 30 )  :: NEXT I 
  380 CALL VCHAR ( 1 , 32 , 140 , 24 ) 
  390 CALL HCHAR ( A1 , B1 , 32 ) 
  400 CALL HCHAR ( A , B , 42 ) 
  410 IF T = 0 THEN 450
  420 X = - X  :: T = 0  :: IF Z < > 30 THEN 450
  430 CALL SOUND ( 200 , 880 , 5 )  :: P = P + 1  :: TR = TR + 1 
  440 GOSUB 620
  450 A1 = A  :: B1 = B 
  460 C = A + X  :: D = B + Y 
  470 IF C < 4 THEN X = - X 
  480 IF C > 24 THEN X = - X  :: P = P - 4  :: CALL SOUND ( 200 , 220 , 5 )  :: GOSUB 620
  490 IF D < 2 OR D > 31 THEN B = B + Y  :: Y = - Y 
  500 CALL HCHAR ( 21 , V1 , 32 , 3 ) 
  510 CALL HCHAR ( 21 , V , 96 , 3 ) 
  520 V1 = V 
  530 CALL JOYST ( 1 , I , J ) 
  540 CALL KEY ( 0 , K , S )  :: IF K = 15 OR TR > = 240 THEN 650
  550 V = V + I / 2  :: IF V < 2 THEN V = 2 
  560 IF V > 29 THEN V = 29 
  570 CALL GCHAR ( A + X , B + Y , Z ) 
  580 IF Z = 32 THEN 600
  590 T = 1 
  600 A = A + X  :: B = B + Y 
  610 GOTO 390
  620 IF P < 0 THEN P = 0 
  630 A$ = STR$ ( P )  :: DISPLAY AT ( 2 , 10 ) SIZE ( LEN ( A$ ) + 1 )  : A$ & CHR$ ( 104 ) 
  640 RETURN 
  650 CALL CLEAR 
  660 CALL COLOR ( 3 , 2 , 8 , 4 , 2 , 8 ) 
  670 CALL SCREEN ( 5 ) 
  680 DISPLAY AT ( 10 , 1 )  : " SIE ERREICHTEN"  ; P  ; "PUNKTE" 
  690 DISPLAY AT ( 23 , 2 )  : "IRGENDEINE TASTE DRUECKEN" 
  700 CALL KEY ( 0 , K , S )  :: IF S < = 0 THEN 700
  710 GOTO 190
  720 CALL CLEAR 
  730 END 
