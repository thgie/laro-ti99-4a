  100 REM   PROGRAM: WORD SAFARI     
  110 REM     VERSION 1 / GERMAN / NOV 1982   
  120 REM    COPYRIGHT 1982 BY TEXAS INSTRUMENTS DEUTSCHLAND GMBH 
  130 CALL CLEAR 
  140 DIM A$ ( 728 ) , O ( 7 ) , W$ ( 25 ) 
  150 DFLAG$ = "0" 
  160 FOR I = 1 TO 8 
  170 CALL COLOR ( I , 2 , 8 ) 
  180 NEXT I 
  190 CALL COLOR ( 9 , 2 , 5 ) 
  200 CALL CHAR ( 96 , "0" ) 
  210 CALL CHAR ( 34 , "3C4299A1A199423C" ) 
  220 CALL CHAR ( 35 , "000000000000FFFF" ) 
  230 CALL CHAR ( 38 , "FFFF" ) 
  240 CALL CHAR ( 36 , "C0C0C0C0C0C0C0C" ) 
  250 CALL CHAR ( 43 , "0303030303030303" ) 
  260 REM  TITLE SCREEN**** 
  270 GOSUB 510
  280 PRINT "        WORT-SAFARI"  :  : "       (WORD SAFARI)"  :  :  :  :  :  : "  "  ; CHR$ ( 34 )  ; " 1982 TEXAS INSTRUMENTS"  :  :  :  :  : 
  290 PRINT "  BELIEBIGE TASTE DRUECKEN" 
  300 GOSUB 1850
  310 REM  MAIN MENU****** 
  320 GOSUB 510
  330 PRINT "         WORT-SAFARI"  :  :  : 
  340 CALL HCHAR ( 19 , 10 , 35 , 14 ) 
  350 CALL HCHAR ( 23 , 10 , 38 , 14 ) 
  360 CALL VCHAR ( 20 , 10 , 36 , 3 ) 
  370 CALL VCHAR ( 20 , 23 , 43 , 3 ) 
  380 PRINT  :  : "       BITTE WAEHLEN SIE" 
  390 PRINT  :  : "   1   WAHL DER AUSGABE" 
  400 PRINT  : "   2   EINGABE DER WORTE" 
  410 PRINT  : "   3   AUSGABE DER WORTE" 
  420 PRINT  : "   4   ANTWORT ZEIGEN" 
  430 PRINT  : "   5   SPIEL BEENDEN"  :  :  : 
  440 GOSUB 1850
  450 IF KEY < 49 THEN 440
  460 IF KEY > 53 THEN 440
  470 ON KEY - 48 GOSUB 1890, 570, 2290, 2090, 490
  480 GOTO 310
  490 CALL CLEAR 
  500 STOP 
  510 REM  SCREEN CLEAR*** 
  520 CALL CLEAR 
  530 CALL SCREEN ( 5 ) 
  540 CALL VCHAR ( 1 , 1 , 96 , 48 ) 
  550 CALL VCHAR ( 1 , 31 , 96 , 48 ) 
  560 RETURN 
  570 REM   NEW GAME****** 
  580 GOSUB 510
  590 PRINT "       BITTE WAEHLEN SIE" 
  600 PRINT  :  :  : "   1   10X10 PUZZLE" 
  610 PRINT  : "   2   20X20 PUZZLE" 
  620 PRINT  : "   3   25X25 PUZZLE"  :  :  :  :  :  :  :  :  : 
  630 GOSUB 1850
  640 IF KEY < 49 THEN 630
  650 IF KEY > 51 THEN 630
  660 SIZE = ( KEY - 48 ) * 10 - 5 * INT ( KEY / 51 ) 
  670 GOSUB 510
  680 C = 1 
  690 C1 = 1 
  700 C2 = 1 
  710 IF FILE = 0 THEN 740
  720 INPUT " NAME DES PUZZLE? "  : TITLE$ 
  730 INPUT " ZAHL DER AUSDRUCKE? "  : C 
  740 INPUT " ZAHL DER WORTE? "  : W 
  750 IF W < 26 THEN 780
  760 PRINT " HOECHSTENS 25 WORTE BITTE." 
  770 GOTO 740
  780 FOR I = 0 TO W - 1 
  790 INPUT " WORT " & STR$ ( I + 1 ) & " ? "  : W$ ( I ) 
  800 IF LEN ( W$ ( I ) ) > 2 THEN 830
  810 PRINT " WENIGSTENS 3 BUCHSTABEN"  : " BITTE" 
  820 GOTO 790
  830 IF LEN ( W$ ( I ) ) < 11 THEN 860
  840 PRINT " HOECHSTENS 10 BUCHSTABEN"  : " BITTE" 
  850 GOTO 790
  860 NEXT I 
  870 PRINT  : " MOECHTEN SIE EIN WORT" 
  880 INPUT " AENDERN? (J/N) "  : S$ 
  890 IF S$ < > "J" THEN 930
  900 INPUT " WORT NUMMER? "  : I 
  910 INPUT " RICHTIGES WORT? "  : W$ ( I - 1 ) 
  920 GOTO 870
  930 GOSUB 510
  940 PRINT  :  :  : "     VORBEREITUNG..." 
  950 O ( 0 ) = 1 
  960 O ( 4 ) = - 1 
  970 O ( 1 ) = SIZE + 1 
  980 O ( 5 ) = - O ( 1 ) 
  990 O ( 2 ) = O ( 1 ) + 1 
 1000 O ( 6 ) = - O ( 2 ) 
 1010 O ( 3 ) = O ( 2 ) + 1 
 1020 O ( 7 ) = - O ( 3 ) 
 1030 I = SIZE + 3 
 1040 J = ( SIZE + 2 ) ^ 2 - 1 - I 
 1050 FOR K = I TO J 
 1060 A$ ( K ) = "  " 
 1070 NEXT K 
 1080 L = SIZE + 2 
 1090 J = L * L - L 
 1100 FOR I = 0 TO J STEP L 
 1110 A$ ( I ) = "**" 
 1120 A$ ( I + SIZE + 1 ) = "**" 
 1130 A$ ( I / L ) = "**" 
 1140 A$ ( J + I / L ) = "**" 
 1150 NEXT I 
 1160 REM  HIDE WORDS*** 
 1170 J = 0 
 1180 FOR N = 0 TO W - 1 
 1190 Q = 0 
 1200 Q = Q + 1 
 1210 S$ = STR$ ( Q ) & " " 
 1220 CALL HCHAR ( 23 , 25 , ASC ( S$ ) ) 
 1230 CALL HCHAR ( 23 , 26 , ASC ( SEG$ ( S$ , 2 , 1 ) ) ) 
 1240 IF Q < 76 THEN 1380
 1250 W = N 
 1260 GOSUB 510
 1270 PRINT " DER COMPUTER KONNTE NUR"  : " "  ; STR$ ( W )  ; " WORTE EINBAUEN!"  :  :  :  :  : 
 1280 PRINT "      BITTE WAEHLEN SIE" 
 1290 PRINT  :  :  : "   1   LISTE MIT "  ; STR$ ( W )  : "        WORTEN BENUTZEN" 
 1300 PRINT  : "   2   NEU ANFANGEN"  :  :  :  :  :  : 
 1310 GOSUB 1850
 1320 IF KEY < > 49 THEN 1360
 1330 GOSUB 510
 1340 PRINT "     VORBEREITUNG..."  :  :  :  :  :  :  :  :  :  :  :  : 
 1350 GOTO 1610
 1360 IF KEY = 50 THEN 1840
 1370 GOTO 1310
 1380 I = INT ( ( ( SIZE + 2 ) ^ 2 - SIZE ) * RND ) + SIZE 
 1390 IF A$ ( I ) < > "  " THEN 1200
 1400 FOR P = 0 TO 7 
 1410 IF J < 8 THEN 1430
 1420 J = 0 
 1430 K = I + O ( J ) 
 1440 FOR M = 2 TO LEN ( W$ ( N ) ) 
 1450 IF K < SIZE + 2 THEN 1500
 1460 IF A$ ( K ) < > "  " THEN 1500
 1470 K = K + O ( J ) 
 1480 NEXT M 
 1490 GOTO 1530
 1500 J = J + 1 
 1510 NEXT P 
 1520 GOTO 1200
 1530 K = I 
 1540 FOR M = 1 TO LEN ( W$ ( N ) ) 
 1550 A$ ( K ) = SEG$ ( W$ ( N ) , M , 1 ) 
 1560 A$ ( K ) = A$ ( K ) & A$ ( K ) 
 1570 K = K + O ( J ) 
 1580 NEXT M 
 1590 J = J + 1 
 1600 NEXT N 
 1610 REM  CAMOUFLAGE**** 
 1620 FOR I = 0 TO ( SIZE + 2 ) ^ 2 - 1 
 1630 IF A$ ( I ) < > "  " THEN 1650
 1640 A$ ( I ) = CHR$ ( INT ( 26 * RND ) + 65 ) & " " 
 1650 NEXT I 
 1660 REM  PRINT PUZZLE*** 
 1670 GOSUB 510
 1680 IF FILE = 0 THEN 1700
 1690 PRINT "    RAETSEL DRUCKEN..." 
 1700 FOR L = 1 TO C 
 1710 IF FILE = 0 THEN 1730
 1720 PRINT # FILE  :  :  :  : "     "  ; TITLE$  : "     ----------"  : 
 1730 FOR I = 0 TO SIZE + 1 
 1740 PRINT # FILE  :  : " "  ; 
 1750 FOR J = I * ( SIZE + 2 ) TO ( I + 1 ) * ( SIZE + 2 ) - 1 
 1760 PRINT # FILE  : SEG$ ( A$ ( J ) , 1 , 1 )  ; 
 1770 NEXT J 
 1780 NEXT I 
 1790 NEXT L 
 1800 IF FILE < > 0 THEN 1830
 1810 PRINT  :  : "  BELIEBIGE TASTE DRUECKEN"  ; 
 1820 GOSUB 1850
 1830 PRINT # FILE  :  :  :  : 
 1840 RETURN 
 1850 REM  CALL KEY**** 
 1860 CALL KEY ( 0 , KEY , ST ) 
 1870 IF ST < = 0 THEN 1860
 1880 RETURN 
 1890 REM  REDIRECT OUTPUT*** 
 1900 GOSUB 510
 1910 PRINT "       BITTE WAEHLEN SIE" 
 1920 PRINT  :  :  : "   1   BILDSCHIRM" 
 1930 PRINT  : "   2   THERMODRUCKER" 
 1940 PRINT  : "   3   ANDERES GERAET"  :  :  :  :  :  :  :  : 
 1950 GOSUB 1850
 1960 IF KEY < 49 THEN 1950
 1970 IF KEY > 51 THEN 1950
 1980 FILE = 0 
 1990 IF KEY = 49 THEN 2080
 2000 FILE = 1 
 2010 DEVICE$ = "TP" 
 2020 IF KEY = 50 THEN 2040
 2030 INPUT " GERAET ? "  : DEVICE$ 
 2040 IF DFLAG$ = "0" THEN 2060
 2050 CLOSE # 1 
 2060 OPEN # 1  : DEVICE$ , OUTPUT 
 2070 DFLAG$ = "1" 
 2080 RETURN 
 2090 REM  PRINT ANSWER*** 
 2100 GOSUB 510
 2110 IF FILE = 0 THEN 2140
 2120 INPUT " ZAHL DER AUSDRUCKE? "  : C1 
 2130 PRINT  :  :  : "    ANTWORT DRUCKEN..." 
 2140 FOR L = 1 TO C1 
 2150 IF FILE = 0 THEN 2170
 2160 PRINT # FILE  :  :  :  : "     "  ; TITLE$  : "     ----------"  : 
 2170 FOR I = 0 TO SIZE + 1 
 2180 PRINT # FILE  :  : " "  ; 
 2190 FOR J = I * ( SIZE + 2 ) TO ( I + 1 ) * ( SIZE + 2 ) - 1 
 2200 PRINT # FILE  : SEG$ ( A$ ( J ) , 2 , 1 )  ; 
 2210 NEXT J 
 2220 NEXT I 
 2230 NEXT L 
 2240 IF FILE < > 0 THEN 2270
 2250 PRINT  :  : "  BELIEBIGE TASTE DRUECKEN"  ; 
 2260 GOSUB 1850
 2270 PRINT # FILE  :  :  :  : 
 2280 RETURN 
 2290 REM  OUTPUT WORDS*** 
 2300 GOSUB 510
 2310 IF FILE = 0 THEN 2340
 2320 INPUT " ZAHL DER AUSDRUCKE? "  : C2 
 2330 PRINT  :  :  : "    WORTE DRUCKEN..." 
 2340 FOR L = 1 TO C2 
 2350 IF FILE = 0 THEN 2370
 2360 PRINT # FILE  :  :  :  : "     "  ; TITLE$  : "     ----------"  :  : 
 2370 FOR I = 0 TO W - 1 
 2380 PRINT # FILE  : "     "  ; W$ ( I ) 
 2390 NEXT I 
 2400 PRINT # FILE  :  :  :  : 
 2410 NEXT L 
 2420 IF FILE < > 0 THEN 2450
 2430 PRINT " BELIEBIGE TASTE DRUECKEN"  ; 
 2440 GOSUB 1850
 2450 RETURN 
